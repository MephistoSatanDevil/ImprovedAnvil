/// actions directed at countering cheaty and cheesy behavior of players ///

// set EnemySeen flag and shout an alert
BEGIN_ACTION_DEFINITION
    Name(AlertEnemySeen)
	TRIGGER
        Global("EnemySeen","LOCALS",0)
	ACTION
		RESPONSE #scsprob1
            SetGlobal("EnemySeen","LOCALS",1)
            Shout(ALERT)
END

// respond to an alert shout
BEGIN_ACTION_DEFINITION
    Name(RespondEnemySeenAlert)
	TRIGGER
        Heard([EVILCUTOFF],ALERT)
        InMyGroup(LastHeardBy(Myself))
        !See([GOODCUTOFF])
        InMyArea(Player1)
	ACTION
		RESPONSE #scsprob1
            MoveToObject(Player1)
END

// activate the flag for party fleeing the immediate area
BEGIN_ACTION_DEFINITION
    Name(SetPartyFled)
	TRIGGER
        Global("partyfled","LOCALS",0)
        !See([GOODCUTOFF])
        !See([PC])
	ACTION
		RESPONSE #scsprob1
            SetGlobalTimer("chase","LOCALS",60)
            SetGlobal("partyfled","LOCALS",1)
END

// reset the flag for party fleeing the immediate area
BEGIN_ACTION_DEFINITION
    Name(ResetPartyFled)
	TRIGGER
        Global("partyfled","LOCALS",1)
	ACTION
		RESPONSE #scsprob1
			SetGlobal("partyfled","LOCALS",0)
END

// initialise anticheese chasing
BEGIN_ACTION_DEFINITION
    Name(InitAntiCheeseChase)
	TRIGGER
        Allegiance(Myself,ENEMY)
        !See([PC])
        !See([GOODCUTOFF])
        Global("EnemySeen","LOCALS",0)
        Global("anticheese","LOCALS",0)
        OR(2)
          AttackedBy([GOODCUTOFF],DEFAULT)
          HPPercentLT(Myself,100)
        InMyArea(Player1)
	ACTION
		RESPONSE #scsprob1
            SetGlobal("anticheese","LOCALS",1)
            SetGlobalTimer("chase2","LOCALS",12)
END

// anticheese chasing timer ran out, heal the creature
BEGIN_ACTION_DEFINITION
    Name(AntiCheeseHeal)
	TRIGGER
        Allegiance(Myself,ENEMY)
        GlobalTimerExpired("chase2","LOCALS")
        Global("anticheese","LOCALS",1)
	ACTION
		RESPONSE #scsprob1
            SetGlobal("anticheese","LOCALS",0)
            ReallyForceSpellRES("SPWII07",Myself)
END

// chase the enemy until the timer is not over
BEGIN_ACTION_DEFINITION
    Name(ChaseParty)
	TRIGGER
        !See([PC])
        !See([GOODCUTOFF])
        GlobalTimerNotExpired("ChaseTimer","LOCALS")
        InMyArea(Player1)
	ACTION
		RESPONSE #scsprob1
			MoveToObject(Player1)
END

// the chase timer ran out, heal the creature as an anticheat measure
BEGIN_ACTION_DEFINITION
    Name(StopeChasingAndHeal)
	TRIGGER
        !See([PC])
        !See([GOODCUTOFF])
        GlobalTimerExpired("chase","LOCALS")
        Global("partyfled","LOCALS",1)
	ACTION
		RESPONSE #scsprob1
            ReallyForceSpellRES("SPWII07",Myself)
END

// PC casts timestop, apply protection from everything
BEGIN_ACTION_DEFINITION
    Name(StartAntiTS)
	TRIGGER
        Global("Iapcastts","GLOBAL",1)
        Global("Iatsinvulner","LOCALS",0)
	ACTION
		RESPONSE #scsprob1
            SetGlobal("Iatsinvulner","LOCALS",1)
            ApplySpellRES("SPINI70",Myself)
END

// PC finished timestop, remove protections
BEGIN_ACTION_DEFINITION
    Name(StopAntiTS)
	TRIGGER
        Global("Iapcastts","GLOBAL",0)
        Global("Iatsinvulner","LOCALS",1)
	ACTION
		RESPONSE #scsprob1
            SetGlobal("Iatsinvulner","LOCALS",0)
            ApplySpellRES("SPINI71",Myself)
END

// someone attacked the creature, turn hostile
BEGIN_ACTION_DEFINITION
    Name(TurnHostile)
	TRIGGER
        AttackedBy([GOODCUTOFF],DEFAULT)
        !Allegiance(Myself,ENEMY)
	ACTION
		RESPONSE #scsprob1
            Enemy()
            Shout(ALERT)
END