/// spell-casting related stuff for AI of the enemy clerics ///

// cast Rigid Thinking
BEGIN_ACTION_DEFINITION
    Name(CastRigidThinking)
    TRIGGER
        Allegiance(Myself,ENEMY)
        !GlobalTimerNotExpired("Iaspell","LOCALS")
        !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
        !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
        !StateCheck(LastSeenBy(Myself),STATE_PANIC)
        CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
        !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
        !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4) // character's not enraged
        !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8) // no chaotic commands
        !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7) // no SI: Enchantment
        !HasItemEquiped("shld25",LastSeenBy(Myself)) // Shield of Harmony
        !HasItemEquiped("leat20",LastSeenBy(Myself)) // Aeger's Hide
        !HasItemEquiped("sw2h14",LastSeenBy(Myself)) // Lilarcor
        !HasItemEquiped("S!helm05",LastSeenBy(Myself)) // Helm of Clarity
        !HasItemEquiped("S!leat03",LastSeenBy(Myself)) // Imp. Corthala Family Armor
        !HasItemEquiped("S!shld01",LastSeenBy(Myself)) // Delryn Family Legacy +4
        !HasItemEquiped("S!sw04",LastSeenBy(Myself)) // Judgement Day
        !HasItemEquiped("S!sw07",LastSeenBy(Myself)) // Warlord's Blade +5
        !HasItemEquiped("S!sw08",LastSeenBy(Myself)) // Warlord's Blade +5
        !HasItemEquiped("S!sw09",LastSeenBy(Myself)) // Warlord's Blade +5
        !HasItemEquiped("S!sw10",LastSeenBy(Myself)) // Warlord's Blade +5
        !HasItemEquiped("S!sw11",LastSeenBy(Myself)) // Warlord's Blade +5
        !HasItemEquiped("S!sw12",LastSeenBy(Myself)) // Warlord's Blade +5
        !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
        !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3) // no Potion of Magic Shielding
        GlobalLT("RigidVar","LOCALS",RigidAmount)
    ACTION
        RESPONSE #100
            IncrementGlobal("RigidVar","LOCALS",1)
            SetGlobalTimer("Iaspell","LOCALS",6)
            SpellNoDec(LastSeenBy(Myself),CLERIC_RIGID_THINKING)
END

// cast Hold
BEGIN_ACTION_DEFINITION
    Name(CastHold)
	TRIGGER
        Allegiance(Myself,ENEMY)
        !GlobalTimerNotExpired("Iaspell","LOCALS")
        !GlobalTimerNotExpired("HoldVar","LOCALS")
        !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
        !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
        !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
        !StateCheck(LastSeenBy(Myself),STATE_PANIC)
        !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7) // no SI: Enchantment
        !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE5) // no Free Action
        !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4) // character's not enraged
        !HasItemEquiped("shld25",LastSeenBy(Myself)) // Shield of Harmony
        !HasItemEquiped("sw1h27",LastSeenBy(Myself)) // Arbane's Sword +2
        !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
        !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3) // no Potion of Magic Shielding
        !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE1) // char is not undead hunter
        !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE1) // char is not inquisitor
        GlobalLT("HoldVar","LOCALS",HoldAmount)
	ACTION
		RESPONSE #scsprob1
            IncrementGlobal("HoldVar","LOCALS",1)
            SetGlobalTimer("Iahold","LOCALS",18)
            SetGlobalTimer("Iaspell","LOCALS",6)
            SpellNoDec(LastSeenBy(Myself),CLERIC_HOLD_PERSON)
END

// cast Cure Medium Wounds
BEGIN_ACTION_DEFINITION
    Name(CastCureMediumWounds)
	TRIGGER
        !GlobalTimerNotExpired("Iaspell","LOCALS")
        InMyArea(Player1)
        HPPercentLT(Myself,50)
        Global("Iacure","LOCALS",0)
	ACTION
		RESPONSE #scsprob1
            SetGlobal("Iacure","LOCALS",1)
            SetGlobalTimer("Iaspell","LOCALS",6)
            SpellNoDec(Myself,CLERIC_CURE_MEDIUM_WOUNDS)
END

// cast True Sight
BEGIN_ACTION_DEFINITION
    Name(CastTrueSight)
	TRIGGER
        Allegiance(Myself,ENEMY)
        !GlobalTimerNotExpired("Iaspell","LOCALS")
        OR(2)
          StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
          StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
	ACTION
		RESPONSE #scsprob1
            SetGlobalTimer("Iaspell","LOCALS",6)
            SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

// cast Command
BEGIN_ACTION_DEFINITION
    Name(CastCommand)
	TRIGGER
        !GlobalTimerNotExpired("Iaspell","LOCALS")
        !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
        !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
        !StateCheck(LastSeenBy(Myself),STATE_PANIC)
        CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
        !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
        !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4) // character's not enraged
        !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8) // no chaotic commands
        !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7) // no SI: Enchantment
        !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
        !HasItemEquiped("morsword",LastSeenBy(Myself))
        !HasItemEquiped("ring95",LastSeenBy(Myself))
        !HasItemEquiped("ring99",LastSeenBy(Myself))
        !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3) // no Potion of Magic Shielding
        GlobalLT("CommandVar","LOCALS",CommandAmount)
	ACTION
		RESPONSE #scsprob1
            IncrementGlobal("CommandVar","LOCALS",1)
            SetGlobalTimer("Iaspell","LOCALS",6)
            SpellNoDec(LastSeenBy(Myself),CLERIC_COMMAND)
END

// cast Greater Command
BEGIN_ACTION_DEFINITION
    Name(CastGreaterCommand)
	TRIGGER
        Allegiance(Myself,ENEMY)
        !GlobalTimerNotExpired("Iaspell","LOCALS")
        !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
        !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
        !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
        !StateCheck(LastSeenBy(Myself),STATE_PANIC)
        CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
        !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4) // character's not enraged
        !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8) // no chaotic commands
        !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7) // no SI: Enchantment
        !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3) // no Potion of Magic Shielding
	ACTION
		RESPONSE #scsprob1
            SetGlobalTimer("Iaspell","LOCALS",6)
            SpellNoDec(LastSeenBy(Myself),CLERIC_GREATER_COMMAND)
END