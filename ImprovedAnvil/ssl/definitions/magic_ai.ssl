/// spell-casting related stuff for AI of the enemy mages ///

// cast vocalize when silenced
BEGIN_ACTION_DEFINITION
    Name(CastVocalize)
	TRIGGER
        Allegiance(Myself,ENEMY)
        StateCheck(Myself,STATE_SILENCED)
	ACTION
		RESPONSE #scsprob1
            SpellNoDec(Myself,WIZARD_VOCALIZE)
END

// cast Ray of Enfeeblement
BEGIN_ACTION_DEFINITION
    Name(CastRayOfEnf)
	TRIGGER
        Allegiance(Myself,ENEMY)
        !StateCheck(Myself,STATE_SILENCED)
        !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
        !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
        !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
        !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
        !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7) // no SI: Enchantment
        !HasBounceEffects(LastSeenBy(Myself))
        Global("Iaenfeeble","LOCALS",0)
	ACTION
		RESPONSE #scsprob1
            SetGlobal("Iaenfeeble","LOCALS",1)
            SpellNoDec(LastSeenBy(Myself),WIZARD_RAY_OF_ENFEEBLEMENT)
END

//cast Magic Missile
BEGIN_ACTION_DEFINITION
    Name(CastMM)
	TRIGGER
        !StateCheck(Myself,STATE_SILENCED)
        GlobalLT("Iamissile","LOCALS",MMAmount)
        !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
        CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
        !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
        CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
        !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7) // no SI: invisibility
    ACTION
        RESPONSE #scsprob1
            IncrementGlobal("Iamissile","LOCALS",1)
            SpellNoDec(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

// cast Melf's Acid Arrow
BEGIN_ACTION_DEFINITION
    Name(CastMelfArrow)
	TRIGGER
        !StateCheck(Myself,STATE_SILENCED)
        !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
        CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
        !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
        !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
        CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
	ACTION
		RESPONSE #scsprob1
            SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

// cast chaos
BEGIN_ACTION_DEFINITION
    Name(CastChaos)
	TRIGGER
        Allegiance(Myself,ENEMY)
        !StateCheck(Myself,STATE_SILENCED)
        !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
        !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
        !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
        !StateCheck(LastSeenBy(Myself),STATE_PANIC)
        CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
        !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
        !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4) // not enraged
        !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8) // no Chaotic Commands
        !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7) // no SI: Enchantment
        !HasItemEquiped("shld25",LastSeenBy(Myself)) // Shield of Harmony
        !HasItemEquiped("leat20",LastSeenBy(Myself)) // Aeger's Hide
        !HasItemEquiped("sw2h14",LastSeenBy(Myself)) // Lilarcor
        Global("Iachaos","LOCALS",0)
	ACTION
		RESPONSE #scsprob1
            SetGlobal("Iachaos","LOCALS",1)
            SpellNoDec(LastSeenBy(Myself),WIZARD_CHAOS)
END

// cast breach from a scroll
BEGIN_ACTION_DEFINITION
    Name(CastBreachFromScroll)
	TRIGGER
        Allegiance(Myself,ENEMY)
        !StateCheck(Myself,STATE_SILENCED)
        HasItem("scrl6u",Myself)
        !StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
        !CheckStat(scstarget,1,SCRIPTINGSTATE7) // no SI: Abjuration
        GlobalLT("Iacastbreach","LOCALS",BreachAmount)
	ACTION
		RESPONSE #scsprob1
            IncrementGlobal("Iacastbreach","LOCALS",1)
            DisplayStringHead(Myself,46153)
            UseItem("scrl6u",scstarget)
END