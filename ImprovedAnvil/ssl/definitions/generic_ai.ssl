/// generic combat AI ///

// skip action when mentally or physically bound
BEGIN_ACTION_DEFINITION
    Name(SkipAction)
	TRIGGER
        Allegiance(Myself,ENEMY)
        OR(6)
            CheckStatGT(Myself,0,SCRIPTINGSTATE9)
            StateCheck(Myself,STATE_CONFUSED)
            StateCheck(Myself,STATE_HELPLESS)
            StateCheck(Myself,STATE_PANIC)
            StateCheck(Myself,STATE_STUNNED)
            Global("Iapcastts","GLOBAL",1)
	ACTION
		RESPONSE #scsprob1
            NoAction()
END

// basic attack for 1 round
BEGIN_ACTION_DEFINITION
    Name(BasicAttack)
	TRIGGER
        Allegiance(Myself,ENEMY)
	ACTION
		RESPONSE #scsprob1
			AttackOneRound(LastSeenBy(Myself))
END

// basic attack for 2 rounds
BEGIN_ACTION_DEFINITION
    Name(BasicAttack2Rounds)
	TRIGGER
        Allegiance(Myself,ENEMY)
	ACTION
		RESPONSE #scsprob1
            AttackOneRound(LastSeenBy(Myself))
            AttackOneRound(LastSeenBy(Myself))
END

// drink a potion
BEGIN_ACTION_DEFINITION
    Name(DrinkPotion)
	TRIGGER
        !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
        HasItem(scsargument1,Myself)
	ACTION
		RESPONSE #scsprob1
            SetGlobalTimer("DrankPotionTimer","LOCALS",6)
            DisplayStringHead(Myself,46150) // ~quaffs a potion~
            UseItem(scsargument1,Myself)
END

// skip drinking a potion
BEGIN_ACTION_DEFINITION
    Name(SkipDrinkingPotion)
	TRIGGER
        Allegiance(Myself,ENEMY)
        !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
	ACTION
		RESPONSE #scsprob1
            SetGlobalTimer("DrankPotionTimer","LOCALS",6)
END