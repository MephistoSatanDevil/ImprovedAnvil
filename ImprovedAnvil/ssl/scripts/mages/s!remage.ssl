/// Mage AI for Slaver and Suna Seni's wizards ///

// Tenth level mage: //
// First level spells (4): Magic Missile x2, Shield, Burning Hands //
// Second level spells (4): Mirror Image, Blur, Melf's Acid Arrow, Ray of Enfeeblement //
// Third level spells (3): Protection from Fire, Slow, Protection from Nomal Missiles //
// Fourth level spells (2): Stoneskin, Minor Globe of Invulnerability //
// Fifth level spells (2): Spell Immunity, Chaos //
// Scrolls: Breach x2, Vocalize x2, Domination //

VARIABLE(BreachAmount=2)
VARIABLE(MMAmount=2)
VARIABLE(ChaseTimer=chase)

INCLUDE FILE(ssl/definitions/anticheat_ai.ssl)
INCLUDE FILE(ssl/definitions/generic_ai.ssl)
INCLUDE FILE(ssl/definitions/magic_ai.ssl)

IF
  Detect([PC])
  Global("prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("prep","LOCALS",1)
    ReallyForceSpell(Myself,WIZARD_SHIELD)
    ReallyForceSpell(Myself,WIZARD_HASTE)
    ReallyForceSpell(Myself,WIZARD_RESIST_FEAR)
    ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
    ReallyForceSpell(Myself,WIZARD_BLUR)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ENCHANTMENT)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
    ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  Global("prep","LOCALS",1)
  HasItem("dragring",Myself)
THEN
  RESPONSE #100
    DestroyItem("dragring")
END

IF TRIGGER
  Target(NearestEnemyOf(Myself))
THEN DO
  Action(ResetPartyFled)
END

IF TRIGGER
  Target(NODEFINEDTARGET)
THEN DO
  Action(SkipAction)
END

IF TRIGGER
  Target(NODEFINEDTARGET)
THEN DO
  Action(CastVocalize)
END

IF
  See(NearestEnemyOfType([GOODCUTOFF.0.0.0.0.SUMMONED]))
  !StateCheck(Myself,STATE_SILENCED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !HasItemEquiped("morsword",LastSeenBy(Myself))
  !HasItemEquiped("ring95",LastSeenBy(Myself))
  HasItem("scrl5n",Myself)
  Global("Iadominate","LOCALS",0)
  !GlobalTimerExpired("Iauseitem","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Iadominate","LOCALS",1)
    SetGlobalTimer("Iauseitem","LOCALS",6)
    DisplayStringHead(Myself,46153)
    UseItem("scrl5n",LastSeenBy(Myself))
END

IF TRIGGER
  TargetBlock(NearestPCsOrdered)
THEN DO
  Action(CastChaos)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  Global("Iaslow","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iaslow","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_SLOW)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  See([PC])
  Range(LastSeenBy(Myself),4)
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  Global("Iaburn","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iaburn","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_BURNING_HANDS)
END

BEGIN LOOP(scsvar||1;2;3;4;5;6)
    IF TRIGGER
        Target(Playerscsvar)
        !GlobalTimerExpired("Iauseitem","LOCALS")
        Global("Iapscsvarnb","GLOBAL",1)
    THEN DO
        SetGlobalTimer("Iauseitem","LOCALS",6)
        SetGlobal("Iapscsvarnb","GLOBAL",0)
        Action(CastBreachFromScroll)
    END
END LOOP

IF TRIGGER
    TargetBlock(ThreeNearestPCsOrdered)
THEN DO
    Action(CastRayOfEnf)
END

IF TRIGGER
    TargetBlock(ThreeNearestEnemiesDesc)
    Global("Iamelf","LOCALS",0)
THEN DO
    SetGlobal("Iamelf","LOCALS",1)
    Action(CastMelfArrow)
END

IF TRIGGER
    TargetBlock(ThreeNearestEnemies)
    !HasItemEquiped("morsword",LastSeenBy(Myself))
THEN DO
    Action(CastMM)
END

IF TRIGGER
  Target(NODEFINEDTARGET)
  Global("prep","LOCALS",1)
  Allegiance(Myself,ENEMY)
THEN DO
  Action(SetPartyFled)
END

IF TRIGGER
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  Allegiance(Myself,ENEMY)
  Global("prep","LOCALS",1)
  Global("partyfled","LOCALS",1)
  Target(NODEFINEDTARGET)
THEN DO
  Action(ChaseParty)
END

IF TRIGGER
  Target(NODEFINEDTARGET)
  Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  Global("prep","LOCALS",1)
THEN DO
  Action(StopeChasingAndHeal)
  SetGlobal("prep","LOCALS",0)
  SetGlobal("Iaburn","LOCALS",0)
  SetGlobal("Iachaos","LOCALS",0)
  SetGlobal("Iaslow","LOCALS",0)
  SetGlobal("Iaenfeeble","LOCALS",0)
  SetGlobal("Iamelf","LOCALS",0)
  SetGlobal("Iamissile","LOCALS",0)
END

IF TRIGGER
  TargetBlock(TwoNearestEnemies)
  ActionListEmpty()
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN DO
  Action(BasicAttack)
END

IF TRIGGER
  Target(NearestEnemyOf(Myself))
  ActionListEmpty()
THEN DO
  Action(BasicAttack)
END

IF
  !See([GOODCUTOFF])
  InMyArea(Player1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END