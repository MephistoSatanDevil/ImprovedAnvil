/// AI script for thieves in random encounters ///

INCLUDE FILE(ssl/definitions/anticheat_ai.ssl)
INCLUDE FILE(ssl/definitions/generic_ai.ssl)

IF TRIGGER
  Target(NODEFINEDTARGET)
  Global("Iapcastts","GLOBAL",1)
THEN DO
  Action(SkipDrinkingPotion)
END

IF TRIGGER
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  TargetBlock(ThreeNearestPCsOrdered)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6) // no physical protections
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE1) // not a barbarian
  !HasItemEquiped("S!sw11",LastSeenBy(Myself)) // no Warlord's Blade with backstab protection
THEN DO
  Action(DrinkPotion,"POTN10") // Potion of Invisibility
END

IF
  !GlobalTimerNotExpired("PoisonWeapon","LOCALS")
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_SILENCED)
  !StateCheck(Myself,STATE_PANIC)
  GlobalLT("Iapweapon","LOCALS",3)
  See(NearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),5)
  !CheckStatGT(LastSeenBy(Myself),99,RESISTPOISON)
THEN
  RESPONSE #100
    IncrementGlobal("Iapweapon","LOCALS",1)
    SetGlobalTimer("PoisonWeapon","LOCALS",20)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpellRES("SPCL423",Myself) //
END

IF TRIGGER
  TargetBlock(ThreeNearestPCsOrdered)
  StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6) // no physical protections
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE1) // not a barbarian
  !HasItemEquiped("S!sw11",LastSeenBy(Myself)) // no Warlord's Blade +5
THEN DO
  Action(BasicAttack)
END

IF TRIGGER
  TargetBlock(ThreeNearestPCsOrdered)
  StateCheck(Myself,STATE_INVISIBLE)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE1)
  !HasItemEquiped("S!sw11",LastSeenBy(Myself))
  OR(2)
   !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
   CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
THEN DO
  Action(BasicAttack)
END

IF
  Allegiance(Myself,ENEMY)
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),2)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF TRIGGER
  TargetBlock(TwoNearestEnemies)
  !Gender(LastSeenBy(Myself),SUMMONED)
  !Race(LastSeenBy(Myself),PLANATAR)
  !Race(LastSeenBy(Myself),DARKPLANATAR)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  !HasItemEquiped("immune2",LastSeenBy(Myself))
THEN DO
  Action(BasicAttack)
END

IF TRIGGER
  TargetBlock(ThreeNearestEnemies)
  !Race(LastSeenBy(Myself),PLANATAR)
  !Race(LastSeenBy(Myself),DARKPLANATAR)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  !HasItemEquiped("immune2",LastSeenBy(Myself))
THEN DO
  Action(BasicAttack)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  InMyArea(Player1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END