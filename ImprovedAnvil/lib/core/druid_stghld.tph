INCLUDE "ImprovedAnvil/lib/tpa/creatures.tpa"
INCLUDE "ImprovedAnvil/lib/tpa/ids.tpa"

APPEND ~SPELL.IDS~ ~2966 LESSER_WEREWOLF_CHANGE~

CLEAR_IDS_MAP

// copy items //

// Anath's note
COPY ~ImprovedAnvil/resources/items/new/S!misc76.itm~ ~override/S!misc76.itm~
  SAY NAME2     @12053
  SAY DESC      @12054

  WRITE_LONG 0x0050 (0 - 1) // unid. desc

// Grancor's note
COPY ~ImprovedAnvil/resources/items/new/S!misc77.itm~ ~override/S!misc77.itm~
  SAY NAME2     @12055
  SAY DESC      @12056

// Jagdu's note
COPY ~ImprovedAnvil/resources/items/new/S!misc70.itm~ ~override/S!misc70.itm~
  SAY NAME2     @12061
  SAY DESC      @12062

// Lost Pages of Balduranâ€™s Logbook
COPY ~ImprovedAnvil/resources/items/new/S!misc71.itm~ ~override/S!misc71.itm~
  SAY NAME2     @12066
  SAY DESC      @12067

COPY ~ImprovedAnvil/resources/items/new/S!werim0.itm~ ~override/S!werim0.itm~

COPY ~ImprovedAnvil/resources/items/new/S!werim1.itm~ ~override/S!werim1.itm~

COPY ~ImprovedAnvil/resources/items/new/S!werim2.itm~ ~override/S!werim2.itm~

COPY ~ImprovedAnvil/resources/items/new/S!werim3.itm~ ~override/S!werim3.itm~

COPY ~ImprovedAnvil/resources/items/new/S!regw1.itm~ ~override/S!regw1.itm~

COPY ~ImprovedAnvil/resources/items/new/S!regw2.itm~ ~override/S!regw2.itm~

COPY ~ImprovedAnvil/resources/items/new/S!regw3.itm~ ~override/S!regw3.itm~

COPY ~ImprovedAnvil/resources/items/new/S!claw01.itm~ ~override/S!claw01.itm~

COPY ~ImprovedAnvil/resources/items/new/S!claw02.itm~ ~override/S!claw02.itm~

COPY ~ImprovedAnvil/resources/items/new/S!claw03.itm~ ~override/S!claw03.itm~

COPY ~ImprovedAnvil/resources/items/new/S!wolf1.itm~ ~override/S!wolf1.itm~

COPY ~ImprovedAnvil/resources/items/new/S!wolf2.itm~ ~override/S!wolf2.itm~

// testing stuff
COPY ~ImprovedAnvil/resources/items/new/SUPRWAND.itm~ ~override/SUPRWAND.itm~
  SAY NAME1 ~Wand of Dispel Everything~
  SAY NAME2 ~Wand of Dispel Everything~

COPY ~ImprovedAnvil/resources/items/new/SWANDDMG.itm~ ~override/SWANDDMG.itm~
  SAY NAME1 ~Wand of Destruction~
  SAY NAME2 ~Wand of Destruction~

COPY ~ImprovedAnvil/resources/items/new/S!bite01.itm~ ~override/S!bite01.itm~

COPY ~ImprovedAnvil/resources/items/new/S!bite02.itm~ ~override/S!bite02.itm~

COPY ~ImprovedAnvil/resources/items/new/S!bite03.itm~ ~override/S!bite03.itm~

// patch items //

// Iron Sigil
COPY_EXISTING ~S!misc43.itm~ ~override/S!misc43.itm~
  SAY DESC      @12065

// Agmantian's Scroll
COPY_EXISTING ~SCRLAG.itm~ ~override/S!misc72.itm~
  SAY NAME1     @12074
  SAY NAME2     @12074
  SAY DESC      @12075

// Mysterious Codex
COPY_EXISTING ~BOOK06.itm~ ~override/S!misc73.itm~
  SAY NAME1     @12078
  SAY NAME2     @12078
  SAY DESC      @12079

  READ_BYTE  0x18 droppable
  WRITE_BYTE 0x18 ("%droppable%" ^^ BIT2)

// copy effects //

COPY ~ImprovedAnvil/resources/effects/S!bite2p.eff~ ~override/S!bite2p.eff~

COPY ~ImprovedAnvil/resources/effects/S!bite3p.eff~ ~override/S!bite3p.eff~

COPY_EXISTING ~SPDEATH.EFF~ ~override/SPDEATH.EFF~
  WRITE_ASCII 0x30 ~WEREGR01~ #8

// install projectiles //

ADD_PROJECTILE ~ImprovedAnvil\resources\projectiles\S!SUPER.PRO~

// copy spells //

COPY ~ImprovedAnvil/resources/spells/S!dru00.spl~ ~override/S!dru00.spl~

COPY ~ImprovedAnvil/resources/spells/S!dru01.spl~ ~override/S!dru01.spl~

COPY ~ImprovedAnvil/resources/spells/S!dru02.spl~ ~override/S!dru02.spl~

COPY ~ImprovedAnvil/resources/spells/S!dru03.spl~ ~override/S!dru03.spl~

COPY ~ImprovedAnvil/resources/spells/S!dru04.spl~ ~override/S!dru04.spl~

COPY ~ImprovedAnvil/resources/spells/S!werhwl.spl~ ~override/S!werhwl.spl~
  SAY NAME1 @12072

  // TODO: this is hardcoded for now, uncomment when moving to the main component
  /*LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 1
      check_headers = 0
      match_opcode = 328
      parameter2 = ~%immune_fear_state%~
  END*/

COPY ~ImprovedAnvil/resources/spells/S!werrag.spl~ ~override/S!werrag.spl~
  SAY NAME1 @12073

COPY ~ImprovedAnvil\resources\spells\SPWII58.spl~ ~override/SPWII58.spl~

COPY ~ImprovedAnvil\resources\spells\SPWII59.spl~ ~override/SPWII59.spl~

COPY ~ImprovedAnvil\resources\spells\SPWII60.spl~ ~override/SPWII60.spl~

COPY ~ImprovedAnvil\resources\spells\SPWII61.spl~ ~override/SPWII61.spl~

COPY ~ImprovedAnvil\resources\spells\SPWII62.spl~ ~override/SPWII62.spl~

// testing stuff
COPY ~ImprovedAnvil\resources\spells\SUPERSPL.spl~ ~override/SUPERSPL.spl~

  SAY NAME1 ~Dispel Everything~

  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR
    header_type = 1
    projectile = ~%S!SUPER%~
  END

	LPF ALTER_EFFECT
		INT_VAR
			check_headers = 1
			match_target = 5
      target = 2
	END

  LPF CLONE_EFFECT
    INT_VAR
      check_globals = 0
      match_opcode = 58
      opcode = 139
      parameter1 = 14056
    STR_VAR
      insert = ~below~
  END

COPY ~ImprovedAnvil\resources\spells\SUPERDMG.spl~ ~override/SUPERDMG.spl~

  SAY NAME1 ~Destroy Everything~

  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR
    header_type = 2
    projectile = ~%S!SUPER%~
  END

// patch spells //

// Lesser Werewolf Change
COPY_EXISTING ~spwi948.spl~ ~override/spwi966.spl~
  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_headers = 1
      match_opcode = 151
    STR_VAR
      resource = ~S!lwer01~
  END

// copy creatures //

COPY ~ImprovedAnvil/resources/creatures/new/S!dracof.cre~         ~override/S!dracof.cre~
 SAY NAME1     @11018
 SAY NAME2     @11018

 ADD_CRE_ITEM ~minhp1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~AMULET~

COPY ~ImprovedAnvil/resources/creatures/new/S!wer01.cre~         ~override/S!wer01.cre~
  SAY NAME1     @12050
  SAY NAME2     @12050

COPY ~ImprovedAnvil/resources/creatures/new/S!wer02.cre~         ~override/S!wer02.cre~
  SAY NAME1     @12051
  SAY NAME2     @12051

COPY ~ImprovedAnvil/resources/creatures/new/S!wer03.cre~         ~override/S!wer03.cre~
  SAY NAME1     @12051
  SAY NAME2     @12051

COPY ~ImprovedAnvil/resources/creatures/new/S!wer04.cre~         ~override/S!wer04.cre~
  SAY NAME1     @12068
  SAY NAME2     @12068

COPY ~ImprovedAnvil/resources/creatures/new/S!wer05.cre~         ~override/S!wer05.cre~
  SAY NAME1     @12068
  SAY NAME2     @12068

COPY ~ImprovedAnvil/resources/creatures/new/S!vert01.cre~         ~override/S!vert01.cre~
  SAY NAME1     @12052
  SAY NAME2     @12052

COPY ~ImprovedAnvil/resources/creatures/new/S!trgu01.cre~         ~override/S!trgu01.cre~
  SAY NAME1     @12057
  SAY NAME2     @12057

COPY ~ImprovedAnvil/resources/creatures/new/S!trgu02.cre~         ~override/S!trgu02.cre~
  SAY NAME1     @12057
  SAY NAME2     @12057

  WRITE_ASCII 0x248 ~S!trgua~ #8 // override script
  WRITE_ASCII 0x280 ~s!trgu02~ #32 // death variable

COPY ~ImprovedAnvil/resources/creatures/new/S!jagdu.cre~         ~override/S!jagdu.cre~
  SAY NAME1     @12058
  SAY NAME2     @12058

  WRITE_ASCII 0x248 ~MAGE16M~ #8 // override script

COPY ~ImprovedAnvil/resources/creatures/new/S!gdru01.cre~         ~override/S!gdru01.cre~
  SAY NAME1     @12059
  SAY NAME2     @12059

  WRITE_ASCII 0x2cc ~iagdru01~ #8 // dialogue

COPY ~ImprovedAnvil/resources/creatures/new/S!gdru02.cre~         ~override/S!gdru02.cre~
  SAY NAME1     @12059
  SAY NAME2     @12059

  WRITE_ASCII 0x248 ~S!GDRU02~ #8 // override script

COPY ~ImprovedAnvil/resources/creatures/new/S!gdru03.cre~         ~override/S!gdru03.cre~
  SAY NAME1     @12059
  SAY NAME2     @12059

  WRITE_ASCII 0x2cc ~iagdru03~ #8 // dialogue

COPY ~ImprovedAnvil/resources/creatures/new/S!gdru04.cre~         ~override/S!gdru04.cre~
  SAY NAME1     @12059
  SAY NAME2     @12059

  WRITE_ASCII 0x2cc ~iagdru04~ #8 // dialogue

COPY ~ImprovedAnvil/resources/creatures/new/S!tslead.cre~         ~override/S!tslead.cre~
  SAY NAME1     @12060
  SAY NAME2     @12060

  WRITE_ASCII 0x2cc ~iatslead~ #8 // dialogue

COPY ~ImprovedAnvil/resources/creatures/new/S!drudiv.cre~         ~override/S!drudiv.cre~
  SAY NAME1     @12083
  SAY NAME2     @12083

COPY ~ImprovedAnvil/resources/creatures/new/S!ferr01.cre~ ~override/S!ferr01.cre~
  SAY NAME1     @12070
  SAY NAME2     @12070

  ADD_CRE_ITEM ~minhp1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~AMULET~

  WRITE_ASCII 0x2cc ~iaferr01~ #8 // dialogue

COPY ~ImprovedAnvil/resources/creatures/new/S!ferr02.cre~ ~override/S!ferr02.cre~
  SAY NAME1     @12070
  SAY NAME2     @12070

  ADD_CRE_ITEM ~minhp1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~AMULET~

  WRITE_ASCII 0x2cc ~iaferr02~ #8 // dialogue

// patch/create creatures //

COPY_EXISTING ~trcut07.cre~ ~override/s!logan.cre~
  WRITE_ASCII 0x280 ~s!logan~ #32 // death variable
  WRITE_ASCII 0x2cc ~~ #8         // dialogue

COPY_EXISTING ~ppdra2.cre~ ~override/s!drad01.cre~
  WRITE_SHORT 0x24 20              // current hp
  WRITE_SHORT 0x26 20              // max hp
  WRITE_ASCII 0x248 ~~ #8          // override script
  WRITE_ASCII 0x250 ~~ #8          // class script
  WRITE_ASCII 0x258 ~~ #8          // race script
  WRITE_ASCII 0x260 ~s!drad01~ #8  // general script
  WRITE_ASCII 0x268 ~~ #8          // default script
  WRITE_ASCII 0x280 ~s!drad01~ #32 // death variable
  WRITE_ASCII 0x2cc ~iadrad~ #8    // dialogue

  REMOVE_CRE_ITEM ~STONSKIN~
  ADD_CRE_ITEM ~minhp1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~AMULET~

COPY_EXISTING ~firamb03.cre~ ~override/firamb03.cre~
  WRITE_SHORT 0x24      40 // current hp
  WRITE_SHORT 0x26      40 // max hp

  REMOVE_CRE_ITEM ~MONHP1~

// dread wolf
COPY_EXISTING ~wolfdr01.cre~ ~override/wolfdr01.cre~
  WRITE_LONG  0x14      10000 // xp
  WRITE_LONG  0x18      8 // xp
  WRITE_SHORT 0x24      100 // current hp
  WRITE_SHORT 0x26      100 // max hp
  WRITE_LONG  0x28      0xe280 // animation
  WRITE_SHORT 0x46      4 // base ac
  WRITE_SHORT 0x48      4 // effective ac
  WRITE_BYTE  0x52      7 // thac0
  WRITE_BYTE  0x53      1 // number of attacks
  WRITE_BYTE  0x54      4 // save vs death
  WRITE_BYTE  0x55      6 // save vs wands
  WRITE_BYTE  0x56      5 // save vs polymorph
  WRITE_BYTE  0x57      4 // save vs breath
  WRITE_BYTE  0x58      7 // save vs spell
  WRITE_BYTE  0x59      20 // resist fire
  WRITE_BYTE  0x5a      80 // resist cold
  WRITE_BYTE  0x5b      80 // resist electricity
  WRITE_BYTE  0x5c      20 // resist acid
  WRITE_BYTE  0x5d      20 // resist magic
  WRITE_BYTE  0x5e      20 // resist magic fire
  WRITE_BYTE  0x5f      80 // resist magic cold
  WRITE_BYTE  0x60      20 // resist slashing
  WRITE_BYTE  0x61      20 // resist crushing
  WRITE_BYTE  0x62      20 // resist piercing
  WRITE_BYTE  0x63      20 // resist missile
  WRITE_BYTE  0x234     13 // level
  WRITE_BYTE  0x238 	  18 // strength
  WRITE_BYTE  0x23c     18 // dexterity
  WRITE_BYTE  0x23d     18 // constitution
  WRITE_BYTE  0x23f     15 // morale
  WRITE_BYTE  0x240      5 // morale break
  WRITE_LONG  0x242     20 // morale recovery
  WRITE_ASCII 0x248 ~s!wolfp1~ #8 // override script
  WRITE_ASCII 0x250 ~~ #8 // class script
  WRITE_ASCII 0x260 ~s!wolf~ #8 // general script
  WRITE_ASCII 0x268 ~~ #8 // default script

  REPLACE_CRE_ITEM ~s!werim0~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~LRING~
  ADD_CRE_ITEM ~s!regw1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~RRING~
  REPLACE_CRE_ITEM ~s!wolf1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON1~

COPY_EXISTING ~wolfdr01.cre~ ~override/wolfdr02.cre~
  WRITE_LONG  0x14      0 // xp

// vampiric wolf
COPY_EXISTING ~wolfva01.cre~ ~override/wolfva01.cre~
  WRITE_LONG  0x14      15000 // xp
  WRITE_LONG  0x18      8 // xp
  WRITE_SHORT 0x24      130 // current hp
  WRITE_SHORT 0x26      130 // max hp
  WRITE_LONG  0x28      0xe280 // animation
  WRITE_SHORT 0x46      2 // base ac
  WRITE_SHORT 0x48      2 // effective ac
  WRITE_BYTE  0x52      7 // thac0
  WRITE_BYTE  0x53      2 // number of attacks
  WRITE_BYTE  0x54      4 // save vs death
  WRITE_BYTE  0x55      6 // save vs wands
  WRITE_BYTE  0x56      5 // save vs polymorph
  WRITE_BYTE  0x57      4 // save vs breath
  WRITE_BYTE  0x58      7 // save vs spell
  WRITE_BYTE  0x59      40 // resist fire
  WRITE_BYTE  0x5a      80 // resist cold
  WRITE_BYTE  0x5b      80 // resist electricity
  WRITE_BYTE  0x5c      40 // resist acid
  WRITE_BYTE  0x5d      40 // resist magic
  WRITE_BYTE  0x5e      40 // resist magic fire
  WRITE_BYTE  0x5f      80 // resist magic cold
  WRITE_BYTE  0x60      40 // resist slashing
  WRITE_BYTE  0x61      40 // resist crushing
  WRITE_BYTE  0x62      40 // resist piercing
  WRITE_BYTE  0x63      40 // resist missile
  WRITE_BYTE  0x234     15 // level
  WRITE_BYTE  0x238 	  18 // strength
  WRITE_BYTE  0x23c     18 // dexterity
  WRITE_BYTE  0x23d     18 // constitution
  WRITE_BYTE  0x23f     15 // morale
  WRITE_BYTE  0x240      5 // morale break
  WRITE_LONG  0x242     20 // morale recovery
  WRITE_ASCII 0x248 ~s!wolfp1~ #8 // override script
  WRITE_ASCII 0x250 ~~ #8 // class script
  WRITE_ASCII 0x260 ~s!wolf~ #8 // general script
  WRITE_ASCII 0x268 ~~ #8 // default script

  REPLACE_CRE_ITEM ~s!werim0~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~LRING~
  REPLACE_CRE_ITEM ~s!regw1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~RRING~
  REPLACE_CRE_ITEM ~s!wolf2~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON1~

// werewolf
COPY_EXISTING ~werewo01.cre~ ~override/werewo01.cre~
  WRITE_LONG  0x14      8000 // xp
  WRITE_LONG  0x18      13 // xp
  WRITE_SHORT 0x24      80 // current hp
  WRITE_SHORT 0x26      80 // max hp
  WRITE_SHORT 0x46      2 // base ac
  WRITE_SHORT 0x48      2 // effective ac
  WRITE_BYTE  0x52      7 // thac0
  WRITE_BYTE  0x53      7 // number of attacks
  WRITE_BYTE  0x54      5 // save vs death
  WRITE_BYTE  0x55      7 // save vs wands
  WRITE_BYTE  0x56      6 // save vs polymorph
  WRITE_BYTE  0x57      5 // save vs breath
  WRITE_BYTE  0x58      8 // save vs spell
  WRITE_BYTE  0x59      20 // resist fire
  WRITE_BYTE  0x5a      80 // resist cold
  WRITE_BYTE  0x5b      80 // resist electricity
  WRITE_BYTE  0x5c      20 // resist acid
  WRITE_BYTE  0x5d      20 // resist magic
  WRITE_BYTE  0x5e      20 // resist magic fire
  WRITE_BYTE  0x5f      80 // resist magic cold
  WRITE_BYTE  0x60      40 // resist slashing
  WRITE_BYTE  0x61      40 // resist crushing
  WRITE_BYTE  0x62      40 // resist piercing
  WRITE_BYTE  0x63      40 // resist missile
  WRITE_BYTE  0x234     15 // level
  WRITE_BYTE  0x238     21 // strength
  WRITE_BYTE  0x23c     18 // dexterity
  WRITE_BYTE  0x23d     18 // constitution
  WRITE_BYTE  0x23f     15 // morale
  WRITE_BYTE  0x240      5 // morale break
  WRITE_LONG  0x242     20 // morale recovery
  WRITE_ASCII 0x248 ~s!werep1~ #8 // override script
  WRITE_ASCII 0x250 ~~ #8 // class script
  WRITE_ASCII 0x260 ~s!were~ #8 // general script

  REPLACE_CRE_ITEM ~s!werim1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~LRING~
  REPLACE_CRE_ITEM ~s!claw01~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON1~
  ADD_CRE_ITEM ~helmnoan~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~HELMET~
  ADD_CRE_ITEM ~s!regw1~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~RRING~
  ADD_CRE_ITEM ~s!bite01~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON2~

COPY_EXISTING ~werewo01.cre~ ~override/S!were01.cre~
  WRITE_LONG  0x14      0 // xp

COPY_EXISTING ~werewo01.cre~ ~override/S!wer01c.cre~
  WRITE_ASCII 0x248 ~~         #8 // override script
  WRITE_ASCII 0x250 ~~         #8 // class script
  WRITE_ASCII 0x280 ~s!wer01c~ #32 // death variable

COPY_EXISTING ~werewo01.cre~ ~override/S!wer02c.cre~
  WRITE_ASCII 0x248 ~~         #8 // override script
  WRITE_ASCII 0x250 ~~         #8 // class script
  WRITE_ASCII 0x280 ~s!wer02c~ #32 // death variable

// young werewolf
COPY_EXISTING ~werewo01.cre~ ~override/S!lwer01.cre~
  WRITE_LONG  0x14      4000 // xp
  WRITE_LONG  0x18      9 // xp
  WRITE_SHORT 0x24      50 // current hp
  WRITE_SHORT 0x26      50 // max hp
  WRITE_SHORT 0x46      6 // base ac
  WRITE_SHORT 0x48      6 // effective ac
  WRITE_BYTE  0x52     11 // thac0
  WRITE_BYTE  0x53      7 // number of attacks (1.5)
  WRITE_BYTE  0x54      5 // save vs death
  WRITE_BYTE  0x55      7 // save vs wands
  WRITE_BYTE  0x56      6 // save vs polymorph
  WRITE_BYTE  0x57      5 // save vs breath
  WRITE_BYTE  0x58      8 // save vs spell
  WRITE_BYTE  0x59      0 // resist fire
  WRITE_BYTE  0x5c      0 // resist acid
  WRITE_BYTE  0x5d      0 // resist magic
  WRITE_BYTE  0x5e      0 // resist magic fire
  WRITE_BYTE  0x234     9 // level
  WRITE_BYTE  0x238     21 // strength
  WRITE_BYTE  0x23c     18 // dexterity
  WRITE_BYTE  0x23d     18 // constitution

  REMOVE_CRE_ITEM ~S!bite01~

  SAY NAME1 @12081
  SAY NAME2 @12081

COPY_EXISTING ~S!lwer01.cre~ ~override/S!lwer02.cre~
  WRITE_LONG  0x14      0 // xp

// greater werewolf
COPY_EXISTING ~WEREGR01.cre~ ~override/weregr01.cre~
              ~RNGWLF04.cre~ ~override/RNGWLF04.cre~
              ~firamb01.cre~ ~override/firamb01.cre~
  WRITE_LONG  0x14      15000 // xp
  WRITE_LONG  0x18      18 // xp
  WRITE_SHORT 0x24      130 // current hp
  WRITE_SHORT 0x26      130 // max hp
  WRITE_SHORT 0x46      0 // base ac
  WRITE_SHORT 0x48      0 // effective ac
  WRITE_BYTE  0x52      3 // thac0
  WRITE_BYTE  0x53      3 // number of attacks
  WRITE_BYTE  0x54      3 // save vs death
  WRITE_BYTE  0x55      5 // save vs wands
  WRITE_BYTE  0x56      4 // save vs polymorph
  WRITE_BYTE  0x57      5 // save vs breath
  WRITE_BYTE  0x58      6 // save vs spell
  WRITE_BYTE  0x59      50 // resist fire
  WRITE_BYTE  0x5a      90 // resist cold
  WRITE_BYTE  0x5b      90 // resist electricity
  WRITE_BYTE  0x5c      50 // resist acid
  WRITE_BYTE  0x5d      50 // resist magic
  WRITE_BYTE  0x5e      50 // resist magic fire
  WRITE_BYTE  0x5f      90 // resist magic cold
  WRITE_BYTE  0x60      60 // resist slashing
  WRITE_BYTE  0x61      60 // resist crushing
  WRITE_BYTE  0x62      60 // resist piercing
  WRITE_BYTE  0x63      60 // resist missile
  WRITE_BYTE  0x234     18 // level
  WRITE_BYTE  0x238     24 // strength
  WRITE_BYTE  0x23c     21 // dexterity
  WRITE_BYTE  0x23d     18 // constitution
  WRITE_BYTE  0x23f     15 // morale
  WRITE_BYTE  0x240      5 // morale break
  WRITE_LONG  0x242     20 // morale recovery

  PATCH_IF !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~firamb01~) BEGIN
    WRITE_ASCII 0x248 ~s!werep2~ #8 // override script
    WRITE_ASCII 0x250 ~~ #8 // class script
    WRITE_ASCII 0x258 ~~ #8 // race script
    WRITE_ASCII 0x260 ~s!were~ #8 // general script
  END

  REPLACE_CRE_ITEM ~s!werim2~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~LRING~
  REPLACE_CRE_ITEM ~s!claw02~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON1~
  ADD_CRE_ITEM ~helmnoan~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~HELMET~
  ADD_CRE_ITEM ~s!regw2~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~RRING~
  ADD_CRE_ITEM ~s!bite02~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON2~

COPY_EXISTING ~WEREGR01.cre~ ~override/S!wereg1.cre~
  WRITE_LONG  0x14      0 // xp

// loup garou
COPY_EXISTING ~WEREGR01.cre~ ~override/S!loup01.cre~
              ~WEREGR01.cre~ ~override/S!loup02.cre~
              ~WEREGR01.cre~ ~override/S!loup03.cre~
  SAY NAME1 @12071
  SAY NAME2 @12071

  WRITE_LONG  0x14      50000 // xp
  WRITE_LONG  0x18      200 // xp
  WRITE_SHORT 0x24      200 // current hp
  WRITE_SHORT 0x26      200 // max hp
  WRITE_SHORT 0x46      (0 - 5) // base ac
  WRITE_SHORT 0x48      (0 - 5) // effective ac
  WRITE_BYTE  0x52      (0 - 4) // thac0
  WRITE_BYTE  0x53      3 // number of attacks
  WRITE_BYTE  0x54      3 // save vs death
  WRITE_BYTE  0x55      5 // save vs wands
  WRITE_BYTE  0x56      4 // save vs polymorph
  WRITE_BYTE  0x57      5 // save vs breath
  WRITE_BYTE  0x58      6 // save vs spell
  WRITE_BYTE  0x59      80 // resist fire
  WRITE_BYTE  0x5a      90 // resist cold
  WRITE_BYTE  0x5b      90 // resist electricity
  WRITE_BYTE  0x5c      80 // resist acid
  WRITE_BYTE  0x5d      80 // resist magic
  WRITE_BYTE  0x5e      80 // resist magic fire
  WRITE_BYTE  0x5f      90 // resist magic cold
  WRITE_BYTE  0x60      70 // resist slashing
  WRITE_BYTE  0x61      70 // resist crushing
  WRITE_BYTE  0x62      70 // resist piercing
  WRITE_BYTE  0x63      90 // resist missile
  WRITE_BYTE  0x234     25 // level
  WRITE_BYTE  0x238     25 // strength
  WRITE_BYTE  0x23c     21 // dexterity
  WRITE_BYTE  0x23d     18 // constitution
  WRITE_BYTE  0x23f     15 // morale
  WRITE_BYTE  0x240      5 // morale break
  WRITE_LONG  0x242     20 // morale recovery

  WRITE_ASCII 0x248 ~s!werep3~ #8 // override script
  WRITE_ASCII 0x250 ~~ #8 // class script
  WRITE_ASCII 0x258 ~~ #8 // race script
  WRITE_ASCII 0x260 ~s!were~ #8 // general script

  LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 101 END
  LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 51 END

  REPLACE_CRE_ITEM ~s!werim3~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~LRING~
  REPLACE_CRE_ITEM ~s!claw03~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON1~
  REPLACE_CRE_ITEM ~s!regw3~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~RRING~
  REPLACE_CRE_ITEM ~s!bite03~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON2~

  PATCH_IF (~%DEST_RES%~ STRING_EQUAL_CASE ~S!loup02~) BEGIN
    WRITE_ASCII 0x280 ~s!loup02~ #32 // death variable
  END

  PATCH_IF (~%DEST_RES%~ STRING_EQUAL_CASE ~S!loup03~) BEGIN
    WRITE_ASCII 0x280 ~s!loup03~ #32 // death variable
  END

ACTION_FOR_EACH ~file~ IN
                ~WEREGR01~
                ~S!wereg1~
                ~RNGWLF04~
                ~firamb01~
BEGIN
  LAF ~S!CHANGE_CREATURE_COLOURS~
    STR_VAR
      file = EVALUATE_BUFFER "%file%"
      opcode = "51"
      //parameter1 = "1600086016"
      parameter1 = "16762880"
  END
END

ACTION_FOR_EACH ~file~ IN
                ~WOLFVA01~
BEGIN
  LAF ~S!CHANGE_CREATURE_COLOURS~
    STR_VAR
      file = EVALUATE_BUFFER "%file%"
      opcode = "51"
      parameter1 = "906032896"
  END
END

ACTION_FOR_EACH ~file~ IN
                ~S!LOUP01~
                ~S!LOUP02~
                ~S!LOUP03~
BEGIN
  LAF ~S!CHANGE_CREATURE_COLOURS~
    STR_VAR
      file = EVALUATE_BUFFER "%file%"
      opcode = "51"
      parameter1 = "1684340480"
  END
END

// patch the scripts //

COPY_EXISTING ~BALDUR.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("GreatDruid","GLOBAL",0)~ ~Global("GreatDruid","GLOBAL",0) Global("IADruidPlot","GLOBAL",0)~
  END

COPY_EXISTING ~RNGWLF01.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(Myself,ANATH_WEREWOLF)~ ~DestroyItem("S!misc76") ForceSpell(Myself,ANATH_WEREWOLF)~
  END

COPY_EXISTING ~RNGWLF05.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Kill(Myself)~ ~GiveItemCreate("S!misc76",Myself,0,0,0) Kill(Myself)~
  END

// make sure the trademeet siege does not overlapse with the other potential Trademeet events
COPY_EXISTING ~AR2000.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("didthis2","AR2000",0)~ ~!Global("IADruidPlot","GLOBAL",4) Global("didthis2","AR2000",0)~
    REPLACE_TEXTUALLY ~Global("JeniaSpawn","AR2000",0)~ ~!Global("IADruidPlot","GLOBAL",4) Global("JeniaSpawn","AR2000",0)~
    REPLACE_TEXTUALLY ~Global("OpenedAR2000Tomb","GLOBAL",0)~ ~!Global("IADruidPlot","GLOBAL",4) Global("OpenedAR2000Tomb","GLOBAL",0)~
    REPLACE_TEXTUALLY ~Global("LorenSpawn","AR2000",0)~ ~!Global("IADruidPlot","GLOBAL",4) Global("LorenSpawn","AR2000",0)~
    REPLACE_TEXTUALLY ~Global("ChaosSpawn","AR2000",0)~ ~!Global("IADruidPlot","GLOBAL",4) Global("ChaosSpawn","AR2000",0)~
  END

COPY_EXISTING ~AR2500.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~RevealAreaOnMap("ar1700")~ ~~
  END

COPY_EXISTING ~CUT41Q.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~True()~ ~!Global("IADruidPlot","GLOBAL",7) !Global("IADradeel","GLOBAL",3)~
    REPLACE_TEXTUALLY ~SmallWait(1)~ ~Placeholder~
    REPLACE_TEXTUALLY ~Wait(1)~ ~Wait(1) Rest() ActionOverride(Player2,Rest()) ActionOverride(Player3,Rest()) ActionOverride(Player4,Rest()) ActionOverride(Player5,Rest()) ActionOverride(Player6,Rest())~
    REPLACE_TEXTUALLY ~Placeholder~ ~SmallWait(1)~
  END

// compile the scripts //

COMPILE ~ImprovedAnvil/scripts/areas/S!0001.baf~
COMPILE ~ImprovedAnvil/scripts/areas/S!0002.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/areas/S!0003.baf~
COMPILE ~ImprovedAnvil/scripts/areas/S!0004.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/areas/S!0005.baf~
COMPILE ~ImprovedAnvil/scripts/areas/S!0006.baf~
COMPILE ~ImprovedAnvil/scripts/areas/S!0007.baf~
COMPILE ~ImprovedAnvil/scripts/areas/S!hidr.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc01.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc02.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc03.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc04.baf~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc05.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc06.baf~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc07.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc08.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc09.baf~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc10.baf~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc11.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc12.baf~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc13.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc14.baf~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc15.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc16.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc17.baf~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!druc18.baf~
COMPILE ~ImprovedAnvil/scripts/cutscenes/S!skipc.baf~
COMPILE ~ImprovedAnvil/scripts/encounters/druid_quests/S!drad01.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/encounters/druid_quests/S!trgua.baf~
COMPILE ~ImprovedAnvil/scripts/encounters/druid_quests/S!gdru02.baf~
COMPILE ~ImprovedAnvil/scripts/lycanthropes/S!were.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/lycanthropes/S!wolf.baf~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/scripts/lycanthropes/S!werep1.baf~
COMPILE ~ImprovedAnvil/scripts/lycanthropes/S!werep2.baf~
COMPILE ~ImprovedAnvil/scripts/lycanthropes/S!werep3.baf~
COMPILE ~ImprovedAnvil/scripts/lycanthropes/S!wolfp1.baf~

// extend the scripts //

// druid grove exterior
<<<<<<<< .../inlined/ar1900.baf
IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",1)
  Global("IADruidAmbush","AR1900",0)
  Global("IADruCut01","AR1900",0)
THEN
  RESPONSE #100
    SetGlobal("IADruCut01","AR1900",1)
    ClearAllActions()
    StartCutSceneMode()
    StartCutSceneEx("S!druc01",TRUE)
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",1)
  Global("IADruidAmbush","AR1900",1)
  Global("IADruCut01","AR1900",1)
  Dead("S!wer01")
  Dead("S!wer02")
  Dead("S!wer03")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IADruidAmbush","AR1900",2)
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",1)
  Global("IADruidAmbush","AR1900",2)
  Global("IADruCut01","AR1900",1)
THEN
  RESPONSE #100
    SetGlobal("IADruCut01","AR1900",2)
    ClearAllActions()
    StartCutSceneMode()
    StartCutSceneEx("S!druc01",TRUE)
END
>>>>>>>>

EXTEND_BOTTOM ~AR1900.BCS~ ~.../inlined/ar1900.baf~

// druid grove interior
<<<<<<<< .../inlined/ar1901.baf
IF
  InMyArea(Player1)
  Global("DruidFight","AR1901",10)
  Global("IADruidPlot","GLOBAL",0)
  Global("GrovePoisoned","GLOBAL",0)
  OR(2)
    Global("Chapter","GLOBAL",2)
    Global("Chapter","GLOBAL",3)
  OR(2)
    Class(Player1,DRUID_ALL)
    Class(Player1,SHAMAN)
  !Class(Player1,FIGHTER_ALL)
THEN
  RESPONSE #100
    SetGlobal("IADruidPlot","GLOBAL",1)
END
>>>>>>>>

EXTEND_TOP ~AR1901.BCS~ ~.../inlined/ar1901.baf~

/**
 * Shadow Temple area
 *
 * Anath's numerous forms, how the item s!misc76 from the druid quest is placed:
 *
 * rngwlf01 - the human form, added to the inventory,
 *            replaces herself with rngwlf04 or rngwlf05 through dialogue, dies if shade lord is finished
 * rngwlf02 - ignored
 * rngwlf03 - added at Kill(Myself); runs into cave at first meeting, or dies near the mirror
 *.rngwlf04 - added to inventory, shapes into this using spell spin876 when she turns enemy and attacks in her cave
 * rngwlf05 - shapes into this from spell spin875, when she turns wolf in her cave and runs away,
              or dies if shade lord is finished
 *
 * To summarize:
 * - add to rngwlf01's inventory through a script, remove at dialogue/script actions if she turns to either werewolf form
 * - add to rngwlf03's inventory through a script
 * - add to rngwlf04's inventory through a script
 * - add to rngwlf05's inventory at Kill(Myself)
 */

<<<<<<<< .../inlined/ar140x.baf
// Anath's human form in the cave
IF
  AreaCheck("AR1403")
  Global("IADruidAnathNote","GLOBAL",0)
  Global("IAItemCheck","AR1403",0)
  Exists("rngwlf01")
  !Dead("rngwlf01")
  !PartyHasItem("S!misc76")
  OR(2)
    Class(Player1,DRUID_ALL)
    Class(Player1,SHAMAN)
THEN
  RESPONSE #100
    SetGlobal("IAItemCheck","AR1403",1)
    GiveItemCreate("S!misc76","rngwlf01",0,0,0)
END

// Anath's aggressive werewolf form in the cave
IF
  AreaCheck("AR1403")
  Global("IADruidAnathNote","GLOBAL",0)
  Global("IAItemCheck2","AR1403",0)
  Exists("rngwlf04")
  !Dead("rngwlf04")
  !PartyHasItem("S!misc76")
  OR(2)
    Class(Player1,DRUID_ALL)
    Class(Player1,SHAMAN)
THEN
  RESPONSE #100
    SetGlobal("IAItemCheck2","AR1403",1)
    GiveItemCreate("S!misc76","rngwlf04",0,0,0)
END

// Anath's werewolf form near the entrance to Shadow Temple
IF
  AreaCheck("AR1404")
  Global("IADruidAnathNote","GLOBAL",0)
  Global("IAItemCheck3","AR1404",0)
  Global("SpawnAnathHuman","GLOBAL",1)
  Global("AnathSpawn","AR1404",1)
  Exists("rngwlf03")
  !Dead("rngwlf03")
  !PartyHasItem("S!misc76")
  OR(2)
    Class(Player1,DRUID_ALL)
    Class(Player1,SHAMAN)
THEN
  RESPONSE #100
    SetGlobal("IAItemCheck3","AR1404",1)
    GiveItemCreate("S!misc76","rngwlf03",0,0,0)
END

IF
  OR(2)
    AreaCheck("AR1403")
    AreaCheck("AR1404")
  Global("IADruidAnathNote","GLOBAL",0)
  PartyHasItem("S!misc76")
THEN
  RESPONSE #100
    SetGlobal("IADruidAnathNote","GLOBAL",1)
END

IF
  OR(2)
    AreaCheck("AR1403")
    AreaCheck("AR1404")
  Global("IADruidAnathNote","GLOBAL",1)
  Global("IADruidPlot","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("IADruidAnathNote","GLOBAL",2)
    AddJournalEntry(@5511,QUEST)
END

IF
  OR(2)
    AreaCheck("AR1403")
    AreaCheck("AR1404")
  Global("IADruidAnathNote","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("IADruidAnathNote","GLOBAL",2)
END
>>>>>>>>

EXTEND_TOP ~AR1403.BCS~ ~.../inlined/ar140x.baf~
EXTEND_TOP ~AR1404.BCS~ ~.../inlined/ar140x.baf~

// Shadow Temple area, restored
<<<<<<<< .../inlined/ar1400.baf
IF
  InMyArea(Player1)
  Global("IADruidMerellaTalk","GLOBAL",1)
  Global("IADruidPlot","GLOBAL",2)
  Global("IAMerellaCheck","AR1400",0)
THEN
  RESPONSE #100
    SetGlobal("IADruidMerellaTalk","GLOBAL",2)
    SetGlobal("IAMerellaCheck","AR1400",1)
    AddJournalEntry(@5512,QUEST)
END

IF
  InMyArea(Player1)
  Global("IAMerellaCheck","AR1400",0)
  Global("IADruidMerellaTalk","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("IAMerellaCheck","AR1400",1)
    SetGlobal("IADruidMerellaTalk","GLOBAL",2)
END
>>>>>>>>

EXTEND_TOP ~AR1400.BCS~ ~.../inlined/ar1400.baf~

// firkraag's lair dungeon
<<<<<<<< .../inlined/ar1202.baf
IF
  InMyArea(Player1)
  Global("IADruidGrancorNote","GLOBAL",0)
  Global("IAItemCheck","AR1202",0)
  Exists("firwlf01")
  !Dead("firwlf01")
  !PartyHasItem("S!misc77")
  OR(2)
    Class(Player1,DRUID_ALL)
    Class(Player1,SHAMAN)
THEN
  RESPONSE #100
    SetGlobal("IAItemCheck","AR1202",1)
    GiveItemCreate("S!misc77","firwlf01",0,0,0)
END

IF
  InMyArea(Player1)
  Global("IADruidGrancorNote","GLOBAL",0)
  Global("IAItemCheck","AR1202",1)
  PartyHasItem("S!misc77")
THEN
  RESPONSE #100
    SetGlobal("IADruidGrancorNote","GLOBAL",1)
    SetGlobal("IAItemCheck","AR1202",2)
END

IF
  InMyArea(Player1)
  Global("IADruidGrancorNote","GLOBAL",1)
  Global("IAItemCheck","AR1202",2)
  Global("IADruidPlot","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("IADruidGrancorNote","GLOBAL",2)
    SetGlobal("IAItemCheck","AR1202",3)
    AddJournalEntry(@5513,QUEST)
END

IF
  InMyArea(Player1)
  Global("IADruidGrancorNote","GLOBAL",1)
  Global("IAItemCheck","AR1202",2)
THEN
  RESPONSE #100
    SetGlobal("IAItemCheck","AR1202",3)
    SetGlobal("IADruidGrancorNote","GLOBAL",2)
END
>>>>>>>>

EXTEND_TOP ~AR1202.BCS~ ~.../inlined/ar1202.baf~

// Trademeet
<<<<<<<< .../inlined/ar2000.baf
IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",4)
  Global("IADruidSiege","GLOBAL",0)
  Global("IADruCut","AR2000",0)
THEN
  RESPONSE #100
    SetGlobal("IADruCut","AR2000",1)
    ClearAllActions()
    StartCutSceneMode()
    StartCutScene("S!druc02")
END
>>>>>>>>

EXTEND_BOTTOM ~AR2000.BCS~ ~.../inlined/ar2000.baf~

// Brynnlaw
<<<<<<<< .../inlined/ar1600.baf
IF
  InMyArea(Player1)
  GlobalLT("IADruidPlot","GLOBAL",6)
  Global("IADruidFailed","GLOBAL",0)
THEN
	RESPONSE #100
    SetGlobal("IADruidFailed","GLOBAL",1)
		SetGlobal("IADruidPlot","GLOBAL",16)
    EraseJournalEntry(@5510)
    EraseJournalEntry(@5511)
    EraseJournalEntry(@5512)
    EraseJournalEntry(@5513)
    EraseJournalEntry(@5514)
    EraseJournalEntry(@5515)
    EraseJournalEntry(@5516)
    EraseJournalEntry(@5517)
    AddJournalEntry(@5518,QUEST_DONE)
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",7)
  Global("IADradeel","GLOBAL",2)
  Global("IASpawn","AR1600",0)
  Global("StealShip","GLOBAL",1)
  Global("cd_saemon_spawn","MYAREA",1)
THEN
	RESPONSE #100
    SetGlobal("IASpawn","AR1600",1)
    CreateCreatureObject("S!drad01","ppsaem3",0,0,0)
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",7)
  Global("IADradeel","GLOBAL",3)
  Global("AsylumPlot","GLOBAL",78)
  Dead("ppsaem3")
THEN
  RESPONSE #100
    SetGlobal("IADruidPlot","GLOBAL",17)
    SetGlobal("IADradeel","GLOBAL",7)
END
>>>>>>>>

EXTEND_BOTTOM ~AR1600.BCS~ ~.../inlined/ar1600.baf~

// Sahuagin city / Underdark (quest failure state)
<<<<<<<< .../inlined/ar2300.baf
IF
  InMyArea(Player1)
  OR(2)
    GlobalLT("IADruidPlot","GLOBAL",8)
    Global("IADruidPlot","GLOBAL",17)
  Global("IADruidFailed","GLOBAL",0)
THEN
	RESPONSE #100
    SetGlobal("IADruidFailed","GLOBAL",1)
    EraseJournalEntry(@5510)
    EraseJournalEntry(@5511)
    EraseJournalEntry(@5512)
    EraseJournalEntry(@5513)
    EraseJournalEntry(@5514)
    EraseJournalEntry(@5515)
    EraseJournalEntry(@5516)
    EraseJournalEntry(@5517)
    EraseJournalEntry(@5519)
    EraseJournalEntry(@5520)
    EraseJournalEntry(@5521)
    EraseJournalEntry(@5522)
    EraseJournalEntry(@5523)
    AddJournalEntry(@5524,QUEST_DONE)
END
>>>>>>>>

EXTEND_BOTTOM ~AR2300.BCS~ ~.../inlined/ar2300.baf~
EXTEND_BOTTOM ~AR2100.BCS~ ~.../inlined/ar2300.baf~

// Brynnlaw tavern
<<<<<<<< .../inlined/ar1602.baf
IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",7)
  Global("IADradeel","GLOBAL",1)
  Global("IASpawn","AR1602",0)
  Global("create","AR1602",1)
THEN
	RESPONSE #100
    SetGlobal("IASpawn","AR1602",1)
    CreateCreatureObject("S!drad01","ppsaem3",0,0,0)
END
>>>>>>>>

EXTEND_BOTTOM ~AR1602.BCS~ ~.../inlined/ar1602.baf~

// Spellhold, prison cell area
<<<<<<<< .../inlined/ar1515.baf
IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",6)
  Global("IATalk","AR1515",1)
THEN
	RESPONSE #100
    SetGlobal("IATalk","AR1515",2)
    AddJournalEntry(@5519,QUEST)
END
>>>>>>>>

EXTEND_BOTTOM ~AR1515.BCS~ ~.../inlined/ar1515.baf~

// Exit from Underdark
// TODO: push it below the first OnCreation() block somehow?
<<<<<<<< .../inlined/ar2500.baf
IF
  OnCreation()
  Global("RevealMap","ar2500",0)
  OR(2)
    Global("IADruidPlot","GLOBAL",0)
    GlobalGT("IADruidPlot","GLOBAL",15)
THEN
  RESPONSE #100
    RevealAreaOnMap("ar1700")
    Continue()
END

IF
  OnCreation()
  Global("IADruidPlot","GLOBAL",8)
THEN
  RESPONSE #100
    SetGlobal("IADruidPlot","GLOBAL",9)
    AddJournalEntry(@5525,QUEST)
    Continue()
END
>>>>>>>>

EXTEND_TOP ~AR2500.BCS~ ~.../inlined/ar2500.baf~

// Small Teeth Pass
<<<<<<<< .../inlined/ar1700.baf
IF
  InMyArea(Player1)
  OnCreation()
  Global("IADruidPlot","GLOBAL",9)
  Global("IASpawns","AR1700",0)
THEN
  RESPONSE #100
    Explore()
    DayNight(DAWN_END)
    MoveViewObject(Player1,INSTANT)
    SetGlobal("IASpawns","AR1700",1)
    SpawnPtDeactivate("Spawn Point 1")
    SpawnPtDeactivate("Spawn Point 2")
    SpawnPtDeactivate("Spawn Point 3")
    SpawnPtDeactivate("Spawn Point 6")
    SpawnPtDeactivate("Spawn Ankheg")
    Continue()
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",9)
  Global("IASpawns","AR1700",1)
THEN
  RESPONSE #100
    SetGlobal("IASpawns","AR1700",2)
    CreateCreature("s!tslead",[3295.275],N)
    ClearAllActions()
    StartCutSceneMode()
    Wait(2)
    ActionOverride("s!tslead",StartDialogNoSet(Player1))
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",10)
  Global("IAfight","AR1700",1)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IAfight","AR1700",2)
    ClearAllActions()
    StartCutSceneMode()
    StartCutSceneEx("S!druc11",TRUE)
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",10)
  Global("IAfight","AR1700",3)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IAfight","AR1700",4)
    RevealAreaOnMap("S!0001")
    ClearAllActions()
    StartCutSceneMode()
    StartCutSceneEx("S!druc11",TRUE)
END
>>>>>>>>

EXTEND_TOP ~AR1700.BCS~ ~.../inlined/ar1700.baf~

// Firkraag's Lair
<<<<<<<< .../inlined/ar1203.baf
IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",11)
  Global("Chapter","GLOBAL",6)
  Global("Iaonce","AR1203",0)
THEN
  RESPONSE #100
    SetGlobal("Iaonce","AR1203",1)
    ActionOverride("druidq",CreateItem("S!MISC72",0,0,0)) //
    ContainerEnable("druidq",TRUE)
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",11)
  Global("Chapter","GLOBAL",6)
  Global("Iaonce","AR1203",1)
  PartyHasItem("S!MISC72")
  Exists("firkra02")
  !Dead("firkra02")
  !Allegiance("firkra02",ENEMY)
THEN
  RESPONSE #100
    SetGlobal("Iaonce","AR1203",2)
    ChangeEnemyAlly("firkra02",ENEMY)
END

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",11)
  Global("Chapter","GLOBAL",6)
  PartyHasItem("S!MISC72")
  Dead("firkra02")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IADruidPlot","GLOBAL",12)
END
>>>>>>>>

EXTEND_BOTTOM ~AR1203.BCS~ ~.../inlined/ar1203.baf~

// BALDUR.BCS
<<<<<<<< .../inlined/baldur.baf
IF
  OR(2)
    Global("Chapter","GLOBAL",2)
    Global("Chapter","GLOBAL",3)
  Global("IADruidPlot","GLOBAL",2)
  GlobalGT("IADruidAnathNote","GLOBAL",0)
  GlobalGT("IADruidMerellaTalk","GLOBAL",0)
  GlobalGT("IADruidGrancorNote","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("IADruidPlot","GLOBAL",3)
    AddJournalEntry(@5514,QUEST)
END
>>>>>>>>

EXTEND_TOP ~BALDUR.BCS~ ~.../inlined/baldur.baf~

// Dradeel, combat script during fight with Irenicus
<<<<<<<< .../inlined/s!ppdra2.baf
IF
  Global("IADruidPlot","GLOBAL",7)
  Global("IADradeel","GLOBAL",0)
  Name("PPDRA2",Myself)
THEN
  RESPONSE #100
    SetGlobal("IADradeel","GLOBAL",1)
    DisplayStringHead(Myself,@5483)
    ForceSpell(Myself,DRYAD_TELEPORT)
		Wait(1)
		DestroySelf()
END
>>>>>>>>

EXTEND_TOP ~s!ppdra2.BCS~ ~.../inlined/s!ppdra2.baf~

// Cutscene to Sahuagin city
<<<<<<<< .../inlined/CUT41Q.baf
IF
	Global("IADruidPlot","GLOBAL",7)
	Global("IADradeel","GLOBAL",3)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ClearAllActions()

		SetInterrupt(FALSE)
		FadeToColor([20.0],0)
		Wait(1)

    ActionOverride("s!drad01",DestroySelf())
    Rest()
    ActionOverride(Player2,Rest())
    ActionOverride(Player3,Rest())
    ActionOverride(Player4,Rest())
    ActionOverride(Player5,Rest())
    ActionOverride(Player6,Rest())
    Wait(1)

		LeaveAreaLUAPanic("S!0002","",[4220.3195],W)
		LeaveAreaLUA("S!0002","",[4220.3195],W)
		ActionOverride(Player2,LeaveAreaLUA("S!0002","",[4240.3265],W))
		ActionOverride(Player3,LeaveAreaLUA("S!0002","",[4360.3235],W))
		ActionOverride(Player4,LeaveAreaLUA("S!0002","",[4445.3190],W))
		ActionOverride(Player5,LeaveAreaLUA("S!0002","",[4735.3240],W))
		ActionOverride(Player6,LeaveAreaLUA("S!0002","",[4790.3375],W))

    MoveViewObject(Myself,INSTANT)
		Explore()
    DayNight(DAWN_END)

    Wait(3)
    FadeFromColor([20.0],0)
    Wait(3)

		SetInterrupt(TRUE)
    MultiPlayerSync()
		EndCutSceneMode()
		SmallWait(1)
END
>>>>>>>>

EXTEND_BOTTOM ~CUT41Q.BCS~ ~.../inlined/CUT41Q.baf~

// journal stuff //

ADD_JOURNAL @5510 @5511 @5512 @5513 @5514 @5515 @5516 @5517 @5518 @5519 @5520 @5521 @5522 @5523 @5524 @5525 @5526 @5527 @5528 @5529
  USING ~%LANGUAGE%druidq.tra~

// patch the dialogues //

COPY_EXISTING ~CECHALLE.DLG~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~True()~ ~Global("IADruidPlot","GLOBAL",0)~
    REPLACE_TEXTUALLY ~Global("GreatDruid","GLOBAL",1)~ ~Global("GreatDruid","GLOBAL",1) Global("IADruidPlot","GLOBAL",0)~
  END

COPY_EXISTING ~AEWERE5.DLG~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~WEREWOLF_CHANGE~ ~LESSER_WEREWOLF_CHANGE~
  END

<<<<<<<< .../inlined/cechalle.d
APPEND ~CECHALLE~
  IF WEIGHT #2 ~OR(2) Global("IADruidPlot","GLOBAL",1) Global("IADruidPlot","GLOBAL",15)~
    THEN BEGIN 17
    SAY @5320
    IF ~~ THEN EXIT
  END

  IF WEIGHT #3 ~OR(2) Global("IADruidPlot","GLOBAL",2) Global("IADruidPlot","GLOBAL",3) Global("IACluesFirstTalk","LOCALS",0)~
    THEN BEGIN 18
    SAY @5380
    IF ~~ THEN REPLY @5381 DO ~SetGlobal("IACluesFirstTalk","LOCALS",1)~ GOTO 20
  END

  IF WEIGHT #4 ~OR(2) Global("IADruidPlot","GLOBAL",2) Global("IADruidPlot","GLOBAL",3) Global("IACluesFirstTalk","LOCALS",1)~
    THEN BEGIN 19
    SAY @5382
    IF ~~ THEN REPLY @5383 GOTO 20
  END

  IF ~~ THEN BEGIN 20
    SAY @5384

    // first clue
    IF ~GlobalGT("IADruidAnathNote","GLOBAL",0) GlobalLT("IADruidAnathNote","GLOBAL",3)~ THEN
      REPLY @5386
      DO ~SetGlobal("IADruidAnathNote","GLOBAL",3)~
      GOTO 21

    // second clue
    IF ~GlobalGT("IADruidMerellaTalk","GLOBAL",0) GlobalLT("IADruidMerellaTalk","GLOBAL",3)~ THEN
      REPLY @5388
      DO ~SetGlobal("IADruidMerellaTalk","GLOBAL",3)~
      GOTO 22

    // third clue
    IF ~GlobalGT("IADruidGrancorNote","GLOBAL",0) GlobalLT("IADruidGrancorNote","GLOBAL",3)~ THEN
      REPLY @5390
      DO ~SetGlobal("IADruidGrancorNote","GLOBAL",3)~
      GOTO 23

    // no clues anymore, still looking for more
    IF ~OR(3) GlobalLT("IADruidAnathNote","GLOBAL",3) GlobalLT("IADruidMerellaTalk","GLOBAL",3) GlobalLT("IADruidGrancorNote","GLOBAL",3)~ THEN REPLY @5392 GOTO 24

    // no clues anymore, all clues found
    IF ~Global("IADruidPlot","GLOBAL",3) Global("IADruidAnathNote","GLOBAL",3) Global("IADruidMerellaTalk","GLOBAL",3) Global("IADruidGrancorNote","GLOBAL",3)~ THEN REPLY @5392 GOTO 25
  END

  IF ~~ THEN BEGIN 21
    SAY @5387
    IF ~~ THEN REPLY @5385 GOTO 20
  END

  IF ~~ THEN BEGIN 22
    SAY @5389
    IF ~~ THEN REPLY @5385 GOTO 20
  END

  IF ~~ THEN BEGIN 23
    SAY @5391
    IF ~~ THEN REPLY @5385 GOTO 20
  END

  IF ~~ THEN BEGIN 24
    SAY @5393
    IF ~~ THEN EXIT
  END

  IF ~~ THEN BEGIN 25
    SAY @5394
    IF ~~ THEN GOTO 26
  END

  IF ~~ THEN BEGIN 26
    SAY @5395
    IF ~~ THEN GOTO 27
  END

  IF ~~ THEN BEGIN 27
    SAY @5396
    IF ~~ THEN REPLY @5397 GOTO 28
  END

  IF ~~ THEN BEGIN 28
    SAY @5398
    IF ~~ THEN DO ~SetGlobal("IADruidPlot","GLOBAL",4) AddJournalEntry(@5515,QUEST)~
    EXIT
  END

  IF WEIGHT #5 ~Global("IADruidPlot","GLOBAL",4)~
    THEN BEGIN 29
    SAY @5399
    IF ~~ THEN EXIT
  END

  IF WEIGHT #6 ~GlobalGT("IADruidPlot","GLOBAL",4) GlobalLT("IADruidPlot","GLOBAL",9)~
    THEN BEGIN 30
    SAY @5460
    IF ~~ THEN GOTO 31
  END

  IF ~~ THEN BEGIN 31
    SAY @5461
    IF ~~ THEN REPLY @5462 GOTO 32
  END

  IF ~~ THEN BEGIN 32
    SAY @5463
    IF ~~ THEN REPLY @5464 GOTO 33
  END

  IF ~~ THEN BEGIN 33
    SAY @5465
    IF ~~ THEN EXIT
  END

  IF WEIGHT #7 ~Global("IADruidPlot","GLOBAL",9) Global("IAAmbushFirstTalk","LOCALS",0)~
    THEN BEGIN 34
    SAY @5740
    IF ~~ THEN REPLY @5741 GOTO 35
  END

  IF ~~
    THEN BEGIN 35
    SAY @5742
    IF ~~ THEN REPLY @5743 GOTO 36
  END

  IF ~~
    THEN BEGIN 36
    SAY @5744
    IF ~~ THEN REPLY @5745 GOTO 37
  END

  IF ~~
    THEN BEGIN 37
    SAY @5746
    IF ~~ THEN REPLY @5747 GOTO 38
  END

  IF ~~
    THEN BEGIN 38
    SAY @5748
    IF ~~ THEN DO ~RevealAreaOnMap("ar1700") SetGlobal("IAAmbushFirstTalk","LOCALS",1)~
    EXIT
  END

  IF WEIGHT #8 ~GlobalGT("IADruidPlot","GLOBAL",8) GlobalLT("IADruidPlot","GLOBAL",15) Global("IAAmbushFirstTalk","LOCALS",1)~
    THEN BEGIN 39
    SAY @5748
    IF ~~ THEN EXIT
  END

  IF WEIGHT #9 ~OR(2) Global("IADruidPlot","GLOBAL",16) Global("IADruidPlot","GLOBAL",17) Global("IAFailedFirstTalk","LOCALS",0)~
    THEN BEGIN 40
    SAY @5750
    IF ~~ THEN REPLY @5751 GOTO 41
  END

  IF ~~
    THEN BEGIN 41
    SAY @5752
    IF ~~ THEN REPLY @5753 GOTO 42
  END

  IF ~~
    THEN BEGIN 42
    SAY @5754
    IF ~~ THEN REPLY @5755 GOTO 43
  END

  IF ~~
    THEN BEGIN 43
    SAY @5756
    IF ~~ THEN REPLY @5757 DO ~SetGlobal("IAFailedFirstTalk","LOCALS",1)~
    EXIT
  END

  IF WEIGHT #10 ~OR(2) Global("IADruidPlot","GLOBAL",16) Global("IADruidPlot","GLOBAL",17) Global("IAFailedFirstTalk","LOCALS",1)~
    THEN BEGIN 44
    SAY @5756
    IF ~~ THEN EXIT
  END
END
>>>>>>>>

COMPILE ~.../inlined/cechalle.d~ USING ~%LANGUAGE%druidq.tra~

COPY_EXISTING ~RNGWLF01.DLG~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,ANATH_WEREWOLF2)~ ~DestroyItem("S!misc76") ReallyForceSpell(Myself,ANATH_WEREWOLF2)~
  END

COPY_EXISTING ~FIRWLF01.DLG~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~wolfwe02~ ~werewo01~
    REPLACE_TEXTUALLY ~GREATER_WOLFWERE_CHANGE~ ~GREATER_WEREWOLF_CHANGE~
    REPLACE_TEXTUALLY ~WOLFWERE_CHANGE~ ~WEREWOLF_CHANGE~
    REPLACE_TEXTUALLY ~ForceSpell(Myself,GREATER_WEREWOLF_CHANGE)~ ~OpenDoor("DOOR43") OpenDoor("DOOR44") ForceSpell(Myself,GREATER_WEREWOLF_CHANGE)~
  END

COPY_EXISTING ~FIRAMB01.DLG~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Enemy()~ ~ChangeAIScript("s!werep2",OVERRIDE) ChangeAIScript("s!were",GENERAL) ChangeAIScript("",DEFAULT) Enemy()~
  END

COPY_EXISTING ~UHRANG01.DLG~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~True()~ ~NumTimesTalkedTo(0) !Class(Player1,DRUID_ALL) !Class(Player1,SHAMAN)~
  END

COPY_EXISTING ~PPSAEM3.DLG~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~StartCutSceneEx("Cut41q",FALSE)~ ~StartCutSceneEx("Cut41q",TRUE)~
    REPLACE_TEXTUALLY ~Global("AsylumPlot","GLOBAL",78)~ ~Global("AsylumPlot","GLOBAL",78) !AreaCheck("S!0002")~
  END

// compile the dialogues //

COMPILE ~ImprovedAnvil/resources/dlg/iavert01.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iagdru01.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iagdru02.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iagdru03.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iagdru04.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iatslead.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iadrad.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iaferr01.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iaferr02.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iadracof.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/iadrudiv.d~ USING ~%LANGUAGE%druidq.tra~

COMPILE ~ImprovedAnvil/resources/dlg/uhrang01.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/corneil.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/ppdradee.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/ppdra2.d~ USING ~%LANGUAGE%druidq.tra~
COMPILE ~ImprovedAnvil/resources/dlg/ppsaem3.d~ USING ~%LANGUAGE%druidq.tra~

// copy the resources //

// new map icon
COPY ~ImprovedAnvil/resources/druid/MAPICONS.BAM~ ~override/MAPICONS.BAM~
COPY ~ImprovedAnvil/resources/druid/MOS1000.PVRZ~ ~override/MOS1000.PVRZ~
COPY ~ImprovedAnvil/resources/druid/MOS1001.PVRZ~ ~override/MOS1001.PVRZ~
COPY ~ImprovedAnvil/resources/druid/MOS1002.PVRZ~ ~override/MOS1002.PVRZ~
COPY ~ImprovedAnvil/resources/druid/MOS1003.PVRZ~ ~override/MOS1003.PVRZ~
COPY ~ImprovedAnvil/resources/druid/MOS1004.PVRZ~ ~override/MOS1004.PVRZ~
COPY ~ImprovedAnvil/resources/druid/MOS1005.PVRZ~ ~override/MOS1005.PVRZ~
COPY ~ImprovedAnvil/resources/druid/MOS1006.PVRZ~ ~override/MOS1006.PVRZ~
COPY ~ImprovedAnvil/resources/druid/MOS1007.PVRZ~ ~override/MOS1007.PVRZ~

// new search map for AR1700
COPY ~ImprovedAnvil/resources/druid/AR1700SR.bmp~ ~override/AR1700SR.bmp~

// new areas
COPY ~ImprovedAnvil/resources/druid/s!0001.are~ ~override/s!0001.are~
COPY ~ImprovedAnvil/resources/druid/S!0001.MOS~ ~override/S!0001.MOS~
COPY ~ImprovedAnvil/resources/druid/S!0001.WED~ ~override/S!0001.WED~
COPY ~ImprovedAnvil/resources/druid/S!0001.tis~ ~override/S!0001.tis~
COPY ~ImprovedAnvil/resources/druid/S!0001HT.BMP~ ~override/S!0001HT.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0001LM.BMP~ ~override/S!0001LM.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0001SR.BMP~ ~override/S!0001SR.BMP~
COPY ~ImprovedAnvil/resources/druid/S000100.PVRZ~ ~override/S000100.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000101.PVRZ~ ~override/S000101.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000102.PVRZ~ ~override/S000102.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000103.PVRZ~ ~override/S000103.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000104.PVRZ~ ~override/S000104.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000105.PVRZ~ ~override/S000105.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000106.PVRZ~ ~override/S000106.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000107.PVRZ~ ~override/S000107.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000108.PVRZ~ ~override/S000108.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000109.PVRZ~ ~override/S000109.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000110.PVRZ~ ~override/S000110.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000111.PVRZ~ ~override/S000111.PVRZ~

COPY ~ImprovedAnvil/resources/druid/S!0002.are~ ~override/S!0002.are~
COPY ~ImprovedAnvil/resources/druid/S!0002.MOS~ ~override/S!0002.MOS~
COPY ~ImprovedAnvil/resources/druid/S!0002.WED~ ~override/S!0002.WED~
COPY ~ImprovedAnvil/resources/druid/S!0002.tis~ ~override/S!0002.tis~
COPY ~ImprovedAnvil/resources/druid/S!0002HT.BMP~ ~override/S!0002HT.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0002LM.BMP~ ~override/S!0002LM.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0002SR.BMP~ ~override/S!0002SR.BMP~
COPY ~ImprovedAnvil/resources/druid/S000200.PVRZ~ ~override/S000200.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000201.PVRZ~ ~override/S000201.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000202.PVRZ~ ~override/S000202.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000203.PVRZ~ ~override/S000203.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000204.PVRZ~ ~override/S000204.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000205.PVRZ~ ~override/S000205.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000206.PVRZ~ ~override/S000206.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000207.PVRZ~ ~override/S000207.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000208.PVRZ~ ~override/S000208.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000209.PVRZ~ ~override/S000209.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000210.PVRZ~ ~override/S000210.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000211.PVRZ~ ~override/S000211.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000212.PVRZ~ ~override/S000212.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000213.PVRZ~ ~override/S000213.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000214.PVRZ~ ~override/S000214.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000215.PVRZ~ ~override/S000215.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000216.PVRZ~ ~override/S000216.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000217.PVRZ~ ~override/S000217.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000218.PVRZ~ ~override/S000218.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000219.PVRZ~ ~override/S000219.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000220.PVRZ~ ~override/S000220.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000221.PVRZ~ ~override/S000221.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000222.PVRZ~ ~override/S000222.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000223.PVRZ~ ~override/S000223.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000224.PVRZ~ ~override/S000224.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000225.PVRZ~ ~override/S000225.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000226.PVRZ~ ~override/S000226.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000227.PVRZ~ ~override/S000227.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000228.PVRZ~ ~override/S000228.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000229.PVRZ~ ~override/S000229.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000230.PVRZ~ ~override/S000230.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000231.PVRZ~ ~override/S000231.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000232.PVRZ~ ~override/S000232.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000233.PVRZ~ ~override/S000233.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000234.PVRZ~ ~override/S000234.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000235.PVRZ~ ~override/S000235.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000236.PVRZ~ ~override/S000236.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000237.PVRZ~ ~override/S000237.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000238.PVRZ~ ~override/S000238.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000239.PVRZ~ ~override/S000239.PVRZ~

COPY ~ImprovedAnvil/resources/druid/WTSWAM.TIS~ ~override/WTSWAM.TIS~
COPY ~ImprovedAnvil/resources/druid/WSWAM00.PVRZ~ ~override/WSWAM00.PVRZ~

COPY ~ImprovedAnvil/resources/druid/S!0003.are~ ~override/S!0003.are~
COPY ~ImprovedAnvil/resources/druid/AR1505.MOS~ ~override/S!0003.MOS~
COPY ~ImprovedAnvil/resources/druid/S!0003.WED~ ~override/S!0003.WED~
COPY ~ImprovedAnvil/resources/druid/AR1505.tis~ ~override/S!0003.tis~
COPY ~ImprovedAnvil/resources/druid/AR1505HT.BMP~ ~override/S!0003HT.BMP~
COPY ~ImprovedAnvil/resources/druid/AR1505LM.BMP~ ~override/S!0003LM.BMP~
COPY ~ImprovedAnvil/resources/druid/AR1505SR.BMP~ ~override/S!0003SR.BMP~
COPY ~ImprovedAnvil/resources/druid/A150500.PVRZ~ ~override/S000300.PVRZ~

COPY ~ImprovedAnvil/resources/druid/S!0004.are~ ~override/S!0004.are~
COPY ~ImprovedAnvil/resources/druid/S!0004.MOS~ ~override/S!0004.MOS~
COPY ~ImprovedAnvil/resources/druid/S!0004.WED~ ~override/S!0004.WED~
COPY ~ImprovedAnvil/resources/druid/S!0004.tis~ ~override/S!0004.tis~
COPY ~ImprovedAnvil/resources/druid/S!0004HT.BMP~ ~override/S!0004HT.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0004LM.BMP~ ~override/S!0004LM.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0004SR.BMP~ ~override/S!0004SR.BMP~
COPY ~ImprovedAnvil/resources/druid/S000400.PVRZ~ ~override/S000400.PVRZ~

COPY ~ImprovedAnvil/resources/druid/S!0005.are~ ~override/S!0005.are~
COPY ~ImprovedAnvil/resources/druid/S!0005.MOS~ ~override/S!0005.MOS~
COPY ~ImprovedAnvil/resources/druid/S!0005.WED~ ~override/S!0005.WED~
COPY ~ImprovedAnvil/resources/druid/S!0005.tis~ ~override/S!0005.tis~
COPY ~ImprovedAnvil/resources/druid/S!0005HT.BMP~ ~override/S!0005HT.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0005LM.BMP~ ~override/S!0005LM.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0005SR.BMP~ ~override/S!0005SR.BMP~
COPY ~ImprovedAnvil/resources/druid/S000500.PVRZ~ ~override/S000500.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000501.PVRZ~ ~override/S000501.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000502.PVRZ~ ~override/S000502.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000503.PVRZ~ ~override/S000503.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000504.PVRZ~ ~override/S000504.PVRZ~
COPY ~ImprovedAnvil/resources/druid/S000505.PVRZ~ ~override/S000505.PVRZ~

COPY ~ImprovedAnvil/resources/druid/S!0006.are~ ~override/S!0006.are~
COPY ~ImprovedAnvil/resources/druid/S!0006.WED~ ~override/S!0006.WED~
COPY ~ImprovedAnvil/resources/druid/S!0006.tis~ ~override/S!0006.tis~
COPY ~ImprovedAnvil/resources/druid/S!0006HT.BMP~ ~override/S!0006HT.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0006LM.BMP~ ~override/S!0006LM.BMP~
COPY ~ImprovedAnvil/resources/druid/S!0006SR.BMP~ ~override/S!0006SR.BMP~
COPY ~ImprovedAnvil/resources/druid/S000600.PVRZ~ ~override/S000600.PVRZ~

COPY ~ImprovedAnvil/resources/druid/S!0007.are~ ~override/S!0007.are~

COPY ~ImprovedAnvil/resources/druid/amb_e10a.wav~ ~override/amb_e10a.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e05a.wav~ ~override/amb_e05a.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e06a.wav~ ~override/amb_e06a.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e24.wav~ ~override/amb_e24.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e25.wav~ ~override/amb_e25.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e04b.wav~ ~override/amb_e04b.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e33b.wav~ ~override/amb_e33b.wav~
COPY ~ImprovedAnvil/resources/druid/amb_m28a.wav~ ~override/amb_m28a.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e05b.wav~ ~override/amb_e05b.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e05d.wav~ ~override/amb_e05d.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e05e.wav~ ~override/amb_e05e.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e05h.wav~ ~override/amb_e05h.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e05f.wav~ ~override/amb_e05f.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e05g.wav~ ~override/amb_e05g.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e10a.wav~ ~override/amb_e10a.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e21.wav~ ~override/amb_e21.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e21a.wav~ ~override/amb_e21a.wav~
COPY ~ImprovedAnvil/resources/druid/amb_e21d.wav~ ~override/amb_e21d.wav~
COPY ~ImprovedAnvil/resources/druid/AMB_N10A.WAV~ ~override/AMB_N10A.WAV~
COPY ~ImprovedAnvil/resources/druid/AM_5000.WAV~ ~override/AM_5000.WAV~
COPY ~ImprovedAnvil/resources/druid/AMBDCWND.WAV~ ~override/AMBDCWND.WAV~

// patch the areas //

COPY_EXISTING ~AR1700.ARE~ ~override/AR1700.ARE~
  // add entrance point at the south-western edge of Small Teeth Pass
  LPF fj_are_structure
    INT_VAR
      fj_loc_x = 640
      fj_loc_y = 3030
      fj_orientation = 8
    STR_VAR
      fj_name = ~ExitSW~
      fj_structure_type = entrance
  END

  // replace ankheg actors with a proper spawn point
  LPF ALTER_AREA_ACTOR
    INT_VAR
      expiry = 0
    STR_VAR
      actor_name = ~Ankheg~
  END

  LPF fj_are_structure
    INT_VAR
      fj_loc_x      = 635
      fj_loc_y      = 2605
      fj_spawn_num  = 1
      fj_difficulty = 2
      fj_delay      = 1
      fj_method     = 2
      fj_max_num    = 3
    STR_VAR
      fj_structure_type = spawn
      fj_name           = ~Spawn Ankheg~
      fj_cre_resref0    = ~ANKHEG01~
      fj_cre_resref1    = ~ANKHEG01~
      fj_cre_resref2    = ~ANKHEG01~
  END
BUT_ONLY

// add a hidden container to Firkraag's area
COPY_EXISTING ~AR1203.ARE~ ~override/AR1203.ARE~
  LPF fj_are_structure
    INT_VAR
      fj_loc_x       = 2245
      fj_loc_y       = 1685
      fj_type        = 8
      fj_flags       = 0b00100000
      fj_lock_diff   = 0
      fj_trap_active = 0
      fj_box_left    = 2238
      fj_box_top     = 1678
      fj_box_right   = 2261
      fj_box_bottom  = 1695
      fj_vertex_0    = 2261 + (1678 << 16)
      fj_vertex_1    = 2261 + (1695 << 16)
      fj_vertex_2    = 2238 + (1695 << 16)
      fj_vertex_3    = 2238 + (1678 << 16)
    STR_VAR
      fj_name = ~druidq~
      fj_structure_type = container
  END
BUT_ONLY

// update the new area (Helter Steps)
COPY_EXISTING ~s!0001.are~ ~override/s!0001.are~
  LPF fj_are_structure
    INT_VAR
      fj_delete_mode = 3
    STR_VAR
      fj_structure_type = actor
  END

  LPF fj_are_structure
    INT_VAR
    fj_loc_x       = 80
    fj_loc_y       = 410
    fj_orientation = 10   // NW
    STR_VAR
    fj_structure_type = entrance
    fj_name           = ExitNW
  END

  LPF fj_are_structure
    INT_VAR
    fj_loc_x       = 4000
    fj_loc_y       = 80
    fj_orientation = 0    // S
    STR_VAR
    fj_structure_type = entrance
    fj_name           = ExitNE
  END

  LPF fj_are_structure
    INT_VAR
      fj_type       = 1
      fj_box_left   = 785
      fj_box_top    = 1865
      fj_box_right  = 845
      fj_box_bottom = 1900
      fj_loc_x      = 800
      fj_loc_y      = 1890
      fj_cursor_idx = 8
      fj_vertex_0   = (785 + (1860 << 16))
      fj_vertex_1   = (845 + (1865 << 16))
      fj_vertex_2   = (840 + (1910 << 16))
      fj_vertex_3   = (785 + (1900 << 16))
    STR_VAR
      fj_name           = ~Statue~
      fj_reg_script     = ~S!STATUE~
      fj_structure_type = region
  END

  LPF ~ALTER_AREA_REGION~
    INT_VAR
      info_point = RESOLVE_STR_REF (@12069)
    STR_VAR
      region_name = ~Cave1~
  END

  LPF ~ALTER_AREA_REGION~
    INT_VAR
      info_point = RESOLVE_STR_REF (@12069)
    STR_VAR
      region_name = ~Cave2~
  END
BUT_ONLY

// update the new area (Balduran's Isle)
COPY_EXISTING ~s!0002.are~ ~override/s!0002.are~
  LPF fj_are_structure
    INT_VAR
      fj_loc_x       = 4198
      fj_loc_y       = 1655
      fj_note_strref = RESOLVE_STR_REF(@12063)
      fj_strref_loc  = 1
      fj_color       = 0
      fj_note_id     = 0
    STR_VAR
      fj_structure_type = note
  END

  LPF fj_are_structure
    INT_VAR
      fj_loc_x       = 4123
      fj_loc_y       = 619
      fj_note_strref = RESOLVE_STR_REF(@12064)
      fj_strref_loc  = 1
      fj_color       = 0
      fj_note_id     = 0
    STR_VAR
      fj_structure_type = note
  END
BUT_ONLY

// update the new area (Final Confrontation Chamber)
COPY_EXISTING ~S!0005.are~ ~override/S!0005.are~
  LPF fj_are_structure
    INT_VAR
      fj_loc_x       = 79
      fj_loc_y       = 977
      fj_note_strref = RESOLVE_STR_REF(@12076)
      fj_strref_loc  = 1
      fj_color       = 0
      fj_note_id     = 0
    STR_VAR
      fj_structure_type = note
  END
BUT_ONLY

// update the new area (Mysterious Book Storage)
COPY_EXISTING ~S!0006.are~ ~override/S!0006.are~
  LPF fj_are_structure
    INT_VAR
      fj_loc_x       = 603
      fj_loc_y       = 375
      fj_note_strref = RESOLVE_STR_REF(@12077)
      fj_strref_loc  = 1
      fj_color       = 0
      fj_note_id     = 0
    STR_VAR
      fj_structure_type = note
  END

  LPF ~ADD_AREA_ITEM~
    STR_VAR
      item_to_add = ~s!misc73~ // Mysterious Codex
      container_to_add_to = ~1~
  END
BUT_ONLY

// update the list of master areas
APPEND ~mastarea.2da~ ~S!0001     value~
APPEND ~mastarea.2da~ ~S!0002     value~
APPEND ~mastarea.2da~ ~S!0005     value~
APPEND ~mastarea.2da~ ~S!0007     value~

// patch the worldmap //

OUTER_SET str_name_ref = RESOLVE_STR_REF (@12082)
ACTION_GET_STRREF str_name_ref str_name

LAF sc#addWmpAre
  INT_VAR mapIcon         = 35
          xCoord          = 864
          yCoord          = 534
          tTime           = 2
          visible         = 0
          reachable       = 1
          visited         = 0
          visibleAdjacent = 0
          inclSv          = 0
  STR_VAR areName = "S!0001"
          strName = EVALUATE_BUFFER "%str_name%"
          strDesc = ""
END

// from links
ACTION_DEFINE_ARRAY nw_areas BEGIN
  AR0020 AR0300 AR0400 AR0500 AR0700 AR0800 AR0900 AR1000 AR2500
END

ACTION_DEFINE_ARRAY ne_areas_far BEGIN
  AR1100 AR1400 AR1404 AR1300 AR1304 AR2000 AR1200 AR1900 AR1800 AR2600 AR2800
END

ACTION_DEFINE_ARRAY ne_areas_closeby BEGIN
  AR1700
END

// to links
ACTION_DEFINE_ASSOCIATIVE_ARRAY to_areas BEGIN
  AR0020 => ExitNE
  AR0300 => ExitNE
  AR0400 => ExitN
  AR0500 => ExitNE
  AR0700 => ExitE
  AR0800 => ExitN
  AR0900 => ExitNW
  AR1000 => ExitN
  AR2500 => CDExit
  AR1100 => ExitSE
  AR1400 => ExitW
  AR1404 => ExitE
  AR1300 => ExitSE
  AR1304 => ExitSE
  AR2000 => ExitNW
  AR1200 => ExitW
  AR1900 => ExitW
  AR1800 => CDExit
  AR2600 => CDExit
END

// set up the links
COPY_EXISTING ~worldmap.wmp~ ~override~
  PHP_EACH nw_areas AS null => area BEGIN
    LPF ADD_WORLDMAP_LINKS
      INT_VAR
        default_entry  = 1
        distance_scale = 2
      STR_VAR
        from_area = EVAL ~%area%~
        to_area = ~S!0001~
        entry = ~ExitNW~
    END
  END

  PHP_EACH ne_areas_far AS null => area BEGIN
    LPF ADD_WORLDMAP_LINKS
      INT_VAR
        default_entry  = 1
        distance_scale = 2
      STR_VAR
        from_area = EVAL ~%area%~
        to_area = ~S!0001~
        entry = ~ExitNE~
    END
  END

  PHP_EACH ne_areas_closeby AS null => area BEGIN
    LPF ADD_WORLDMAP_LINKS
      INT_VAR
        default_entry  = 1
        distance_scale = 0
      STR_VAR
        from_area = EVAL ~%area%~
        to_area = ~S!0001~
        entry = ~ExitNE~
    END
  END

  PHP_EACH to_areas AS area => entry BEGIN
    LPF ADD_WORLDMAP_LINKS
      INT_VAR
        distance_scale = 2
      STR_VAR
        from_area = ~S!0001~
        to_area = EVAL ~%area%~
        entry = EVAL ~%entry%~
    END
  END

  LPF ADD_WORLDMAP_LINKS
    INT_VAR
      distance_scale = 0
    STR_VAR
      from_area = ~S!0001~
      to_area = ~AR1700~
      entry = ~ExitSW~
  END
BUT_ONLY

// autobuffs //

INCLUDE "ImprovedAnvil/lib/core/autobuffs.tph"
