/// update and patch various IDS lists ///

// update the stats.ids file //

COPY_EXISTING ~stats.ids~ ~override~
  SET num = 2
  FOR (i = 116; i <= 133; i += 1) BEGIN
    REPLACE_TEXTUALLY ~^%i% .*~ ~%i% EXTRAPROFICIENCY%num%~
    num = num + 1
  END

  SET num = 7
  FOR (i = 162; i <= 175; i += 1) BEGIN
    REPLACE_TEXTUALLY ~^%i% .*$~ ~%i% SCRIPTINGSTATE%num%~
    num = num + 1
  END

  REPLACE_TEXTUALLY ~156 SCRIPTINGSTATE1[%LNL%%MNL%%WNL%]+156 WING_BUFFET~ ~156 SCRIPTINGSTATE1~
  REPLACE_TEXTUALLY ~157 SCRIPTINGSTATE2[%LNL%%MNL%%WNL%]+157 DEATH_WARD~ ~157 SCRIPTINGSTATE2~
  REPLACE_TEXTUALLY ~158 SCRIPTINGSTATE3[%LNL%%MNL%%WNL%]+158 LEVEL_DRAIN_IMMUNITY~ ~158 SCRIPTINGSTATE3~
  REPLACE_TEXTUALLY ~159 SCRIPTINGSTATE4[%LNL%%MNL%%WNL%]+159 OFFENSIVE_MODIFIER~ ~159 SCRIPTINGSTATE4~
  REPLACE_TEXTUALLY ~160 SCRIPTINGSTATE5[%LNL%%MNL%%WNL%]+160 DEFENSIVE_MODIFIER~ ~160 SCRIPTINGSTATE5~
  REPLACE_TEXTUALLY ~161 SCRIPTINGSTATE6[%LNL%%MNL%%WNL%]+161 STRENGTH_MODIFIER~ ~161 SCRIPTINGSTATE6~

// Add new IA spells to SPELL.IDS //

APPEND ~SPELL.IDS~ ~1418 CLERIC_ACID_SHIELD~
APPEND ~SPELL.IDS~ ~1218 CLERIC_ALICORN_LANCE~
APPEND ~SPELL.IDS~ ~1445 CLERIC_SPIRITUAL_WEAPON~
APPEND ~SPELL.IDS~ ~1545 CLERIC_ENTROPY_SHIELD~
APPEND ~SPELL.IDS~ ~1546 CLERIC_ENTROPY_SHIELD_CRUSHING~
APPEND ~SPELL.IDS~ ~1547 CLERIC_ENTROPY_SHIELD_MISSILE~
APPEND ~SPELL.IDS~ ~1548 CLERIC_ENTROPY_SHIELD_PIERCING~
APPEND ~SPELL.IDS~ ~1549 CLERIC_ENTROPY_SHIELD_SLASHING~
APPEND ~SPELL.IDS~ ~1649 CLERIC_IMPERVIOUS_SANCTITY_OF_MIND~
APPEND ~SPELL.IDS~ ~1749 CLERIC_STORM_SHELL~
APPEND ~SPELL.IDS~ ~2966 LESSER_WEREWOLF_CHANGE~

COPY_EXISTING ~SPELL.IDS~ ~override~
  REPLACE_TEXTUALLY ~1250 CLERIC_WRITHING_FOG~ ~1250 CLERIC_SOOTHING_FOG~
  REPLACE_TEXTUALLY ~1650 CLERIC_SPIRITUAL_LOCK~ ~1650 CLERIC_PRIMAL_WARD~
  REPLACE_TEXTUALLY ~1750 CLERIC_ETHER_GATE~ ~1750 CLERIC_SENSE_OF_LIMBO~
  BUT_ONLY

// Add new spell states to SPLSTATE.IDS //

LAF resolve_state STR_VAR new_state_id = ~WIZARD_SPELL_TURNING~ RET new_state_index END
OUTER_SET spell_turning_state = %new_state_index%

LAF resolve_state STR_VAR new_state_id = ~IMMUNE_FEAR~ RET new_state_index END
OUTER_SET immune_fear_state = %new_state_index%

LAF resolve_state STR_VAR new_state_id = ~RIGHTEOUS_MAGIC~ RET new_state_index END
OUTER_SET righteous_magic_state = %new_state_index%

LAF resolve_state STR_VAR new_state_id = ~ACID_SHIELD~ RET new_state_index END
OUTER_SET acid_shield_state = %new_state_index%

LAF resolve_state STR_VAR new_state_id = ~PERFORM_RITUAL~ RET new_state_index END
OUTER_SET perform_ritual_state = %new_state_index%

LAF resolve_state STR_VAR new_state_id = ~FLAME_BLADE~ RET new_state_index END
OUTER_SET flame_blade_state = %new_state_index%

LAF resolve_state STR_VAR new_state_id = ~SLOW_IMMUNITY~ RET new_state_index END
OUTER_SET slow_immunity_state = %new_state_index%

// Fix KIT.IDS //

COPY_EXISTING ~kit.ids~ ~override~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(TRUECLASS[ %TAB%%LNL%%MNL%%WNL%]+\)~              ~0x4000 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BERSERKER[ %TAB%%LNL%%MNL%%WNL%]+\)~              ~0x4001 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(WIZARDSLAYER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4002 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(KENSAI[ %TAB%%LNL%%MNL%%WNL%]+\)~                 ~0x4003 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(CAVALIER[ %TAB%%LNL%%MNL%%WNL%]+\)~               ~0x4004 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(INQUISITOR[ %TAB%%LNL%%MNL%%WNL%]+\)~             ~0x4005 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(UNDEADHUNTER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4006 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_ABJURER[ %TAB%%LNL%%MNL%%WNL%]+\)~     ~0x0040 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_CONJURER[ %TAB%%LNL%%MNL%%WNL%]+\)~    ~0x0080 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_DIVINER[ %TAB%%LNL%%MNL%%WNL%]+\)~     ~0x0100 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_ENCHANTER[ %TAB%%LNL%%MNL%%WNL%]+\)~   ~0x0200 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_ILLUSIONIST[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~0x0400 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_INVOKER[ %TAB%%LNL%%MNL%%WNL%]+\)~     ~0x0800 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_NECROMANCER[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~0x1000 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_TRANSMUTER[ %TAB%%LNL%%MNL%%WNL%]+\)~  ~0x2000 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_GENERALIST[ %TAB%%LNL%%MNL%%WNL%]+\)~  ~0x4000 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(FERALAN[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4007 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(STALKER[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4008 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BEASTMASTER[ %TAB%%LNL%%MNL%%WNL%]+\)~            ~0x4009 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(ASSASIN[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x400A \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BOUNTYHUNTER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x400B \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(SWASHBUCKLER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x400C \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BLADE[ %TAB%%LNL%%MNL%%WNL%]+\)~                  ~0x400D \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(JESTER[ %TAB%%LNL%%MNL%%WNL%]+\)~                 ~0x400E \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(SKALD[ %TAB%%LNL%%MNL%%WNL%]+\)~                  ~0x400F \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(GODTALOS[ %TAB%%LNL%%MNL%%WNL%]+\)~               ~0x4013 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(GODHELM[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4014 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(GODLATHANDER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4015 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(TOTEMIC[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4010 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(SHAPESHIFTER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4011 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BEASTFRIEND[ %TAB%%LNL%%MNL%%WNL%]+\)~            ~0x4012 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BARBARIAN[ %TAB%%LNL%%MNL%%WNL%]+\)~              ~0x40000000 \1~
  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(WILDMAGE[ %TAB%%LNL%%MNL%%WNL%]+\)~               ~0x80000000 \1~
  BUT_ONLY

APPEND ~kit.ids~ ~0x4000 TRUECLASS~ UNLESS ~^.+[ %TAB%]TRUECLASS[ %TAB%%LNL%%MNL%%WNL%]+~
APPEND ~kit.ids~ ~0x40000000 BARBARIAN~ UNLESS ~^.+[ %TAB%]BARBARIAN[ %TAB%%LNL%%MNL%%WNL%]+~
APPEND ~kit.ids~ ~0x80000000 WILDMAGE~ UNLESS ~^.+[ %TAB%]WILDMAGE[ %TAB%%LNL%%MNL%%WNL%]+~

CLEAR_IDS_MAP