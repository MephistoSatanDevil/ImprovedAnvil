/// Various patches for BG2:EE areas ///

// Red Wizard Enclave
COPY_EXISTING ~OH6300.ARE~ ~override/OH6300.ARE~
  PATCH_FOR_EACH ~item_to_delete~ IN ~clck01~ ~misc45~ ~wand06~ ~scrl7s~ ~scrl8r~ BEGIN
    LAUNCH_PATCH_MACRO ~DELETE_AREA_ITEM~
  END

  LAUNCH_PATCH_FUNCTION ~ADD_AREA_ITEM~ STR_VAR
    item_to_add = ~ohnrobe3~ // Tunic of blindeye
    container_to_add_to = ~2~
  END

// Deepstone Clanhold, Exterior
COPY_EXISTING ~OH4200.ARE~ ~override/OH4200.ARE~
  PATCH_FOR_EACH ~item_to_delete~ IN ~potn11~ ~potn35~ BEGIN
    LAUNCH_PATCH_MACRO ~DELETE_AREA_ITEM~
  END

// Deepstone Clanhold
COPY_EXISTING ~OH4210.ARE~ ~override/OH4210.ARE~
  PATCH_FOR_EACH ~item_to_delete~ IN ~potn04~ BEGIN
    LAUNCH_PATCH_MACRO ~DELETE_AREA_ITEM~
  END

// Deepstone Mines
COPY_EXISTING ~OH4220.ARE~ ~override/OH4220.ARE~
  READ_LONG 0xC0 "interroffset"
  WRITE_ASCII ("%interroffset%" + 72) ~S!umbhu2~ #8
  WRITE_ASCII ("%interroffset%" + 80) ~S!umbhu2~ #8

// Dragomir's Tomb
COPY_EXISTING ~OH7000.ARE~ ~override/OH7000.ARE~
  PATCH_FOR_EACH ~item_to_delete~ IN ~scrl09~ BEGIN
    LAUNCH_PATCH_MACRO ~DELETE_AREA_ITEM~
  END

// Crypt of Durkon
COPY_EXISTING ~OH7100.ARE~ ~override/OH7100.ARE~
  PATCH_FOR_EACH ~item_to_delete~ IN ~amul23~ ~amul05~ BEGIN
    LAUNCH_PATCH_MACRO ~DELETE_AREA_ITEM~
  END

// Tomb of the Unproved
COPY_EXISTING ~OH7200.ARE~ ~override/OH7200.ARE~
  PATCH_FOR_EACH ~item_to_delete~ IN ~boot07~ BEGIN
    LAUNCH_PATCH_MACRO ~DELETE_AREA_ITEM~
  END

// Korkorran's Lair
COPY_EXISTING ~OH7300.ARE~ ~override/OH7300.ARE~
  PATCH_FOR_EACH ~item_to_delete~ IN ~potn55~ BEGIN
    LAUNCH_PATCH_MACRO ~DELETE_AREA_ITEM~
  END

  LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR
    old_item = ~POTN40~
    new_item = ~POTN11~
  END

// Dark Moon Heretic Temple
<<<<<<<< .../inlined/old-oh4100.baf
IF
	Global("ohr_sharrans_dead","oh4100",0)
	!NumDeadLT("OHRSHRN1",2)
	!NumDeadLT("OHRSHRN2",2)
	!NumDeadLT("OHRSHRN3",2)
	!NumDeadLT("OHRSHRN4",1)
	!NumDeadLT("OHRSHRN5",1)
	!NumDeadLT("OHRSHRN6",5)
THEN
	RESPONSE #100
		SetGlobal("ohr_sharrans_dead","oh4100",1)
		Continue()
END
>>>>>>>>
<<<<<<<< .../inlined/new-oh4100.baf
IF
	Global("ohr_sharrans_dead","oh4100",0)
	Global("OHR_PLOT","GLOBAL",5)
	!NumDeadLT("OHRSHRN5",1)
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("ohr_sharrans_dead","oh4100",1)
		Continue()
END
>>>>>>>>

COPY_EXISTING ~OH4100.BCS~ ~override/OH4100.BCS~
  REPLACE_BCS_BLOCK ~.../inlined/old-oh4100.baf~ ~.../inlined/new-oh4100.baf~
  BUT_ONLY