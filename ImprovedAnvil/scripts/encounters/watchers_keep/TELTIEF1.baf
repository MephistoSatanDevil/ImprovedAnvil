IF
  Allegiance(Myself,ENEMY)
  Global("TiefFight","AR3005",0)
THEN
  RESPONSE #100
    SetGlobal("TiefFight","AR3005",1)
END

IF
  !Allegiance(Myself,ENEMY)
  Global("TiefFight","AR3005",1)
THEN
  RESPONSE #100
    Enemy()
END

IF
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  Global("Iapcastts","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  !GlobalTimerNotExpired("Iaragetimer","LOCALS")
  GlobalLT("Iaenrage","LOCALS",4)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStat(Myself,2,SCRIPTINGSTATE3)
  !CheckStat(Myself,1,SCRIPTINGSTATE4)
  !CheckStatGT(Myself,0,SCRIPTINGSTATE9)
  Detect([PC])
THEN
  RESPONSE #100
    IncrementGlobal("Iaenrage","LOCALS",1)
    SetGlobalTimer("Iaragetimer","LOCALS",92)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpellRES("SPCL321",Myself) //
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  !CheckStat(Myself,1,SCRIPTINGSTATE4)
  Detect([PC])
  HasItem("potn35",Myself)
  Global("Iapotnuse","LOCALS",0)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Iapotnuse","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn35",Myself)
END

IF
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  HasItem("POTN55",Myself) // ~Potion of Superior Healing~
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150) // ~quaffs a potion~
    UseItem("POTN55",Myself) // ~Potion of Superior Healing~
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  See(NearestEnemyOf(Myself))
  Global("GiantStrengthPotion","LOCALS",0)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  HasItem("POTN05",Myself)  // ~Potion of Fire Giant Strength~
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    SetGlobal("GiantStrengthPotion","LOCALS",1)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("POTN05",Myself)  // ~Potion of Fire Giant Strength~
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,SCRIPTINGSTATE9)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  Range(LastSeenBy(Myself),5)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Global("whirlwind","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("whirlwind","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_WHIRLWIND)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  InMyArea(Player1)
  !Allegiance(Myself,ENEMY)
  Allegiance("TELTIEF3",ENEMY)
THEN
  RESPONSE #100
    Enemy()
END