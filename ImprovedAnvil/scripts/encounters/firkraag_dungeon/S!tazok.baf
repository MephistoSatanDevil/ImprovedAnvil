IF
  NumTimesTalkedTo(0)
  Detect([PC])
  Global("Iapcastts","GLOBAL",0)
THEN
  RESPONSE #100
    StartDialogueNoSet([PC])
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Iataz02","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iataz02","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("partyfled","LOCALS",0)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  Global("Iapcastts","GLOBAL",0)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  See([PC])
  HasItem("potn35",Myself)
  Global("Iataz01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iataz01","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn35",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  Global("Iataz01","LOCALS",1)
  HasItem("S!minhp1",Myself)
THEN
  RESPONSE #100
    DestroyItem("S!minhp1")
END

IF
  !HasItem("S!minhp1",Myself)
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  !GlobalGT("Ianotreasure","AR1202",0)
  Detect([PC])
  Global("Iatreasure","LOCALS",0)
  InPartyAllowDead("CERND")
THEN
  RESPONSE #100
    SetGlobal("Iatreasure","LOCALS",1)
    GiveItemCreate("clck05",Myself,0,0,0) //
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,60)
  HasItem("potn55",Myself)
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn55",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  Global("Iapcastts","GLOBAL",0)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !CheckStatGT(Myself,0,SCRIPTINGSTATE9)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  Range(LastSeenBy(Myself),5)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Global("Iataz03","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iataz03","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_WHIRLWIND)
END

IF
  !See([PC])
  !See([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  Global("Iataz02","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",60)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  !See([PC])
  !See([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  Global("Iataz02","LOCALS",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !See([PC])
  !See([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
  Global("Iataz02","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iataz02","LOCALS",0)
    SetGlobal("Iataz03","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  !See([PC])
  !See([GOODCUTOFF])
  Global("Iataz02","LOCALS",0)
  Global("anticheese","LOCALS",0)
  OR(2)
   AttackedBy([GOODCUTOFF],DEFAULT)
   HPPercentLT(Myself,100)
  InMyArea(Player1)
THEN
  RESPONSE #100
    SetGlobal("anticheese","LOCALS",1)
    SetGlobalTimer("chase2","LOCALS",12)
END

IF
  Allegiance(Myself,ENEMY)
  GlobalTimerNotExpired("chase2","LOCALS")
  Global("anticheese","LOCALS",1)
  Global("Iataz02","LOCALS",0)
  !See([PC])
  !See([GOODCUTOFF])
  InMyArea(Player1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  GlobalTimerExpired("chase2","LOCALS")
  Global("anticheese","LOCALS",1)
  Global("Iataz02","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("anticheese","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END