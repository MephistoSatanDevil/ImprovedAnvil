IF
  Range([PC],50)
  NumberOfTimesTalkedTo(0)
  Global("Iapcastts","GLOBAL",0)
  Detect([PC])
  Allegiance(Myself,NEUTRAL)
THEN
  RESPONSE #100
    StartDialogNoSet([PC])
END

IF
  See(NearestEnemyOf(Myself))
  Global("Iaexit","AR0523",0)
THEN
  RESPONSE #100
    SetGlobal("Iaexit","AR0523",1)
END

IF
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Iaturml01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iaturml01","LOCALS",1)
    Shout(89)
END

IF
  Allegiance(Myself,ENEMY)
  OR(2)
   Detect([PC])
   Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("partyfled","LOCALS",0)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  Global("Iapcastts","GLOBAL",0)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  Range(LastSeenBy(Myself),5)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Global("Iaturml02","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iaturml02","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_WHIRLWIND)
END

IF
  !Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  OR(3)
   AttackedBy([GOODCUTOFF],DEFAULT)
   TookDamage()
   HPPercentLT(Myself,100)
THEN
  RESPONSE #100
    Enemy()
    Shout(89)
    MoveToObject(Player1)
END

IF
  Heard([EVILCUTOFF],89)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Shout(89)
    Enemy()
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  HPPercentLT(Myself,50)
  HasItem("potn55",Myself)
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn55",Myself)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  GlobalGT("Iaturml01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",90)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  Global("Iaturml01","LOCALS",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  Global("Iaturml01","LOCALS",1)
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iaturml01","LOCALS",0)
    SetGlobal("Iaturml02","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(ThirdNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Heard([EVILCUTOFF],89)
  ActionListEmpty()
  !See([GOODCUTOFF])
  InMyArea(Player1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  Heard([EVILCUTOFF],89)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  See(LastHeardBy(Myself))
THEN
  RESPONSE #100
    MoveToObject(LastHeardBy(Myself))
END