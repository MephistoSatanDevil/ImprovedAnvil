// Nyalee //
// Druid level 28 //
// First level spells (11): Armor of Faith, CLW x10//
// Second level spells (11): //
// Third level spells (10): ZONE_OF_SWEET_AIR, Dispel Magic x2, Hold Animals x2 //
//                          Miscast Magic x2 //
// Fourth level spells (10): DEATH_WARD, CSW x4 //
// Fifth level spells (10): IRONSKIN x7, True Seeing, MASS_CURE, Insect Plague //
// Sixth level spells (10): PHYSICAL_MIRROR x2, CONJ ANIMALS or Conj Fire El x8 //
// Seventh level spells (6): Aura of F. D., REGENERATE x2, F.o.D, CREEPING D. x2 //

IF
  Detect(Player1)
  Global("TalkedToNyalee","GLOBAL",0)
THEN
  RESPONSE #100
    StartDialogNoSet(Player1)
END

IF
  AttackedBy([GOODCUTOFF],DEFAULT)
  Allegiance(Myself,NEUTRAL)
  Global("NyaleeHeart","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("Attacked","LOCALS",1)
    StartDialogNoSet([PC])
END

IF
  Global("NyaleeHeart","GLOBAL",1)
  HasItem("minhp1",Myself)  // <Invalid Strref -1>
THEN
  RESPONSE #100
    DestroyItem("minhp1")  // <Invalid Strref -1>
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Ianyal01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Ianyal01","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SetGlobalTimer("Ianyaleerest","GLOBAL",2100)
    ReallyForceSpell(Myself,CLERIC_DEATH_WARD)
    ReallyForceSpell(Myself,CLERIC_ARMOR_OF_FAITH)
    ReallyForceSpell(Myself,CLERIC_IRONSKIN)
    ReallyForceSpell(Myself,CLERIC_AURA_OF_FLAMING_DEATH)
    ReallyForceSpell(Myself,CLERIC_PHYSICAL_MIRROR)
    ReallyForceSpell(Myself,CLERIC_CONJURE_ANIMALS)
    ReallyForceSpell(Myself,CLERIC_CONJURE_FIRE_ELEMENTAL)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  GlobalTimerExpired("Ianyaleerest","GLOBAL")
  Global("Ianyal01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("Ianyaleerest","GLOBAL",2100)
    SetGlobal("Ianyal01","LOCALS",0)
    SetGlobal("Ianyal02","LOCALS",0)
    SetGlobal("Ianyal03","LOCALS",0)
    SetGlobal("Ianyal04","LOCALS",0)
    SetGlobal("Ianyal05","LOCALS",0)
    SetGlobal("Ianyal06","LOCALS",0)
    SetGlobal("Ianyal07","LOCALS",0)
    SetGlobal("Ianyal08","LOCALS",0)
    SetGlobal("Ianyal09","LOCALS",0)
    SetGlobal("Ianyal10","LOCALS",0)
    SetGlobal("Ianyal11","LOCALS",0)
    SetGlobal("Ianyal12","LOCALS",0)
    SetGlobal("Ianyal13","LOCALS",0)
    SetGlobal("Ianyal16","LOCALS",0)
    SetGlobal("Ianoward","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  See(NearestEnemyOf(Myself))
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("partyfled","LOCALS",0)
END

IF
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  OR(2)
   CheckStatGT(Myself,0,SCRIPTINGSTATE9)
   Global("Iapcastts","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaspell","LOCALS",6)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("Ianyal04","LOCALS",6)
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal04","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_IRONSKIN)
END

IF
  Allegiance(Myself,ENEMY)
  See([PC])
  !GlobalTimerNotExpired("Iacreature","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("Iacreature","LOCALS",18)
    CreateCreatureObjectDoor("S!spidwh",LastSeenBy(Myself),0,0,0) //
    CreateCreatureObject("S!spider",Myself,0,0,0) //
    CreateCreatureObject("S!spide2",Myself,0,0,0) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  OR(3)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
   StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !GlobalTimerNotExpired("truesight","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("truesight","LOCALS",60)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearest([PC]))
  OR(3)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
   StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !GlobalTimerNotExpired("truesight","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("truesight","LOCALS",60)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(ThirdNearest([PC]))
  OR(3)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
   StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !GlobalTimerNotExpired("truesight","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("truesight","LOCALS",60)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaexrm","LOCALS")
  See([PC])
  OR(2)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaexrm","LOCALS",30)
    SetGlobalTimer("Iaspell","LOCALS",6)
    ForceSpellRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaexrm","LOCALS")
  See(SecondNearest([PC]))
  OR(2)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaexrm","LOCALS",30)
    SetGlobalTimer("Iaspell","LOCALS",6)
    ForceSpellRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaexrm","LOCALS")
  See(ThirdNearest([PC]))
  OR(2)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaexrm","LOCALS",30)
    SetGlobalTimer("Iaspell","LOCALS",6)
    ForceSpellRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaexrm","LOCALS")
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaexrm","LOCALS",30)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDecRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaexrm","LOCALS")
  See(SecondNearest([PC]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaexrm","LOCALS",30)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDecRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaexrm","LOCALS")
  See(ThirdNearest([PC]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaexrm","LOCALS",30)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDecRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  !GlobalTimerNotExpired("truesight","LOCALS")
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  Detect([PC])
  OR(2)
    !See([PC])
    !See([GOODCUTOFF])
THEN
  RESPONSE #100
    SetGlobalTimer("truesight","LOCALS",60)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOfType([0.0.0.0.0.ILLUSIONARY.0]))
  Global("Iaremove","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iaremove","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDecRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapccloud","AR5202",1)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  InMyArea(Player1)
THEN
  RESPONSE #100
    SetGlobal("Iapccloud","AR5202",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    ForceSpell(Myself,CLERIC_ZONE_OF_SWEET_AIR)
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  HPPercentLT(Myself,75)
  GlobalLT("Ianyal02","LOCALS",2)
  !GlobalTimerNotExpired("Iaregentime","LOCALS")
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal02","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SetGlobalTimer("Iaregentime","LOCALS",60)
    SpellNoDec(Myself,CLERIC_REGENERATE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  OR(5)
    StateCheck(LastSeenBy(Myself),STATE_HASTED)
    StateCheck(LastSeenBy(Myself),STATE_DRAWUPONHOLYMIGHT)
    StateCheck(LastSeenBy(Myself),STATE_BLUR)
    StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
    CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  GlobalLT("Ianyal12","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal12","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDecRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOfType([PC.0.0.MAGE_ALL]))
  OR(2)
   CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE2)
   CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  Global("Ianoward","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Ianoward","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDecRES("SPPRI02",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOfType([PC.0.0.MAGE_ALL]))
  Global("Ianyal11","LOCALS",0)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE2)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
THEN
  RESPONSE #100
    SetGlobal("Ianyal11","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_FINGER_OF_DEATH)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
  Global("Ianyal05","LOCALS",0)
  !GlobalTimerNotExpired("Iainsect","LOCALS")
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
THEN
  RESPONSE #100
    SetGlobal("Ianyal05","LOCALS",1)
    SetGlobalTimer("Iainsect","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CREEPING_DOOM)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  Global("Ianyal03","LOCALS",0)
  !GlobalTimerNotExpired("Iainsect","LOCALS")
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
THEN
  RESPONSE #100
    SetGlobal("Ianyal03","LOCALS",1)
    SetGlobalTimer("Iainsect","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CREEPING_DOOM)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !See([GOODCUTOFF])
  HPPercentLT(Myself,75)
  GlobalLT("Ianyal06","LOCALS",3)
  Global("Ianyal02","LOCALS",2)
  !GlobalTimerNotExpired("Iaregentime","LOCALS")
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal06","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
  See(NearestEnemyOf(Myself))
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  HPPercentLT(Myself,75)
  GlobalLT("Ianyal06","LOCALS",3)
  Global("Ianyal02","LOCALS",2)
  !GlobalTimerNotExpired("Iaregentime","LOCALS")
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal06","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iasummons","LOCALS")
  See(NearestEnemyOf(Myself))
  Global("Ianyal09","LOCALS",0)
  Allegiance(Myself,ENEMY)
  RandomNumGT(6,3)
THEN
  RESPONSE #100
    SetGlobal("Ianyal09","LOCALS",1)
    SetGlobalTimer("Iasummons","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CONJURE_FIRE_ELEMENTAL)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaanimal","LOCALS")
  See(NearestEnemyOf(Myself))
  Global("Ianyal09","LOCALS",0)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    SetGlobal("Ianyal09","LOCALS",1)
    SetGlobalTimer("Iaanimal","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CONJURE_ANIMALS)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  Global("Ianyal05","LOCALS",1)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !HasItemEquiped("morsword",LastSeenBy(Myself))
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !GlobalTimerNotExpired("Iainsect","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Ianyal05","LOCALS",2)
    SetGlobalTimer("Iainsect","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_INSECT_PLAGUE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Ianyal08","LOCALS",2)
  !GlobalTimerNotExpired("Iamiscast","LOCALS")
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal08","LOCALS",1)
    SetGlobalTimer("Iamiscast","LOCALS",30)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOfType([PC.0.0.MAGE_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !HasBounceEffects(LastSeenBy(Myself))
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  GlobalLT("Ianyal08","LOCALS",2)
  !GlobalTimerNotExpired("Iamiscast","LOCALS")
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal08","LOCALS",1)
    SetGlobalTimer("Iamiscast","LOCALS",30)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iasummons","LOCALS")
  See(NearestEnemyOf(Myself))
  GlobalLT("Ianyal09","LOCALS",7)
  Allegiance(Myself,ENEMY)
  RandomNumGT(6,3)
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal09","LOCALS",1)
    SetGlobalTimer("Iasummons","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CONJURE_FIRE_ELEMENTAL)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaanimal","LOCALS")
  See(NearestEnemyOf(Myself))
  GlobalLT("Ianyal09","LOCALS",7)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal09","LOCALS",1)
    SetGlobalTimer("Iaanimal","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CONJURE_ANIMALS)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(LastSeenBy(Myself),75)
  HPPercentLT(Myself,75)
  Range(LastSeenBy(Myself),5)
  Global("Ianyal16","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Ianyal16","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_MASS_CURE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(LastSeenBy(Myself),75)
  Range(LastSeenBy(Myself),5)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  Global("Ianyal07","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Ianyal07","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !See([GOODCUTOFF])
  See([EVILCUTOFF])
  HPPercentLT(LastSeenBy(Myself),75)
  HPPercentLT(Myself,75)
  Range(LastSeenBy(Myself),5)
  Global("Ianyal16","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Ianyal16","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_MASS_CURE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !See([GOODCUTOFF])
  See([EVILCUTOFF])
  HPPercentLT(LastSeenBy(Myself),75)
  Range(LastSeenBy(Myself),5)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  Global("Ianyal07","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Ianyal07","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOfType([GOODCUTOFF.ANIMAL.0.0.0.SUMMONED]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  GlobalLT("Ianyal10","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal10","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_HOLD_ANIMAL)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  HPPercentLT(Myself,90)
  GlobalLT("Ianyal13","LOCALS",10)
  Global("Ianyal02","LOCALS",2)
  !GlobalTimerNotExpired("Iaregentime","LOCALS")
THEN
  RESPONSE #100
    IncrementGlobal("Ianyal13","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CURE_LIGHT_WOUNDS)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  Global("Ianyal01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",60)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  !StateCheck(Myself,STATE_SILENCED)
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  Global("Ianyal01","LOCALS",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  !Detect([PC])
  !Detect([GOODCUTOFF])
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
  Global("Ianyal01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Ianyal01","LOCALS",0)
    SetGlobal("Ianyal02","LOCALS",0)
    SetGlobal("Ianyal03","LOCALS",0)
    SetGlobal("Ianyal04","LOCALS",0)
    SetGlobal("Ianyal05","LOCALS",0)
    SetGlobal("Ianyal06","LOCALS",0)
    SetGlobal("Ianyal07","LOCALS",0)
    SetGlobal("Ianyal08","LOCALS",0)
    SetGlobal("Ianyal09","LOCALS",0)
    SetGlobal("Ianyal10","LOCALS",0)
    SetGlobal("Ianyal11","LOCALS",0)
    SetGlobal("Ianyal12","LOCALS",0)
    SetGlobal("Ianyal13","LOCALS",0)
    SetGlobal("Ianyal16","LOCALS",0)
    SetGlobal("Ianoward","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  See(ThirdNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END