IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  Global("Iadoonce","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iadoonce","LOCALS",1)
    SetInterrupt(FALSE)
    ChangeAIScript("",RACE)
    ChangeAIScript("",GENERAL)
    ChangeAIScript("",DEFAULT)
    SetInterrupt(TRUE)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  LevelLT(Myself,18)
  AttackedBy([GOODCUTOFF],DEFAULT)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    Enemy()
    ChangeAIScript("GPSHOUT",RACE)
    ChangeAIScript("MAGE16M",GENERAL)
    ChangeAIScript("WTASIGHT",DEFAULT)
    SetInterrupt(TRUE)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  LevelGT(Myself,17)
  AttackedBy([GOODCUTOFF],DEFAULT)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    Enemy()
    ChangeAIScript("GPSHOUT",RACE)
    ChangeAIScript("MAGE18d",GENERAL)
    ChangeAIScript("WTASIGHT",DEFAULT)
    SetInterrupt(TRUE)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  See(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
    ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_ELECTRICITY)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
    ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
    ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
    ReallyForceSpell(Myself,WIZARD_BLUR)
    ReallyForceSpellRES("SPWII41",Myself)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_DIVINATION)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_STONE_SKIN)
  CheckStatLT(Myself,1,STONESKINS)
  Global("Prep1","LOCALS",0)
  !HPLT(Myself,1)
THEN
  RESPONSE #100
    SetGlobal("Prep1","LOCALS",1)
    DisplayString(Myself,29736)  // ~Contingency - Stoneskin~
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  OR(6)
   CheckStatGT(Myself,0,SCRIPTINGSTATE9)
   StateCheck(Myself,STATE_HELPLESS)
   StateCheck(Myself,STATE_STUNNED)
   StateCheck(Myself,STATE_CONFUSED)
   StateCheck(Myself,STATE_PANIC)
   Global("Iapcastts","GLOBAL",1)
THEN
  RESPONSE #100
    NoAction()
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  See(NearestEnemyOf(Myself))
  StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    SpellNoDec(Myself,WIZARD_VOCALIZE)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  See(NearestEnemyOf(Myself))
  CheckStatLT(Myself,1,STONESKINS)
  Global("Prep1","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Prep1","LOCALS",2)
    SpellNoDec(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  LevelGT(Myself,16)
  See(NearestEnemyOf(Myself))
  CheckStatLT(Myself,1,STONESKINS)
  CheckStat(Myself,0,SCRIPTINGSTATE6)
  Global("Prep1","LOCALS",2)
THEN
  RESPONSE #100
    SetGlobal("Prep1","LOCALS",3)
    SpellNoDec(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  GlobalGT("Prep","LOCALS",0)
  Global("Iainvis","LOCALS",0)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobal("Iainvis","LOCALS",1)
    SpellNoDec(Myself,WIZARD_SHADOW_DOOR)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  LevelLT(Myself,17)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  Global("Iagmalison","LOCALS",1)
  GlobalTimerExpired("Iasequencer","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  Global("Prep","LOCALS",1)
  !HPLT(Myself,1)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",2)
    DisplayString(Myself,39969)  // ~Spell Sequencer - Fired~
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_SLOW)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_EMOTION_HOPELESSNESS)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_CONFUSION)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  LevelLT(Myself,17)
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  Global("Iaeightcast2","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iaeightcast2","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  LevelGT(Myself,16)
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  GlobalLT("Iaeightcast2","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Iaeightcast2","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  LevelLT(Myself,17)
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTELECTRICITY)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  Global("chain","LOCALS",0)
THEN
  RESPONSE #100
    IncrementGlobal("chain","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  LevelGT(Myself,16)
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTELECTRICITY)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("chain","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("chain","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iafarrow","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iafarrow","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iaacid","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iaacid","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !HasItemEquiped("morsword",LastSeenBy(Myself))
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iamissile","LOCALS",5)
THEN
  RESPONSE #100
    IncrementGlobal("Iamissile","LOCALS",1)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

IF
  Allegiance(Myself,GOODBUTBLUE)
  AreaCheck("AR1516")
  !Detect([EVILCUTOFF])
  InMyArea(Player1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END