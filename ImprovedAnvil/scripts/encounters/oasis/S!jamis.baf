IF
  NumberOfTimesTalkedTo(0)
  Global("Iapcastts","GLOBAL",0)
  Detect(Player1)
THEN
  RESPONSE #100
    StartDialogNoSet(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  HasItem("S!minhp1",Myself)
THEN
  RESPONSE #100
    DestroyItem("S!minhp1")
END

IF
  !HasItem("S!minhp1",Myself)
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Iajamis01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iajamis01","LOCALS",1)
    SetGlobalTimer("Iajamisrest","GLOBAL",2100)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  GlobalTimerExpired("Iajamisrest","GLOBAL")
  Global("Iajamis01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("Iajamisrest","GLOBAL",2100)
    SetGlobal("Iajamis01","LOCALS",0)
    SetGlobal("Iajamis02","LOCALS",0)
    SetGlobal("Iajamis03","LOCALS",0)
    SetGlobal("Iajamis04","LOCALS",0)
    SetGlobal("Iajamis05","LOCALS",0)
    SetGlobal("Iajamis06","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  OR(2)
   Detect([PC])
   Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("partyfled","LOCALS",0)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  Global("Iapcastts","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,90)
  GlobalLT("Iajamis05","LOCALS",2)
  CheckStatLT(Myself,71,RESISTCRUSHING)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    IncrementGlobal("Iajamis05","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_HARDINESS)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,60)
  HasItem("potn55",Myself)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn55",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iagww","LOCALS")
  See(NearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),5)
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
  GlobalLT("Iajamis04","LOCALS",4)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    IncrementGlobal("Iajamis04","LOCALS",1)
    SetGlobalTimer("Iagww","LOCALS",12)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_GREATER_WHIRLWIND)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iacstrike","LOCALS")
  See(NearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),5)
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iajamis03","LOCALS",4)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    IncrementGlobal("Iajamis03","LOCALS",1)
    SetGlobalTimer("Iacstrike","LOCALS",18)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_CRITICAL_STRIKE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iagww","LOCALS")
  See(NearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),5)
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
  Global("Iajamis06","LOCALS",0)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Iajamis06","LOCALS",1)
    SetGlobalTimer("Iagww","LOCALS",12)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_WHIRLWIND)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  GlobalGT("Iajamis01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",90)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  GlobalGT("Iajamis01","LOCALS",0)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
  GlobalGT("Iajamis01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iajamis01","LOCALS",0)
    SetGlobal("Iajamis02","LOCALS",0)
    SetGlobal("Iajamis03","LOCALS",0)
    SetGlobal("Iajamis04","LOCALS",0)
    SetGlobal("Iajamis05","LOCALS",0)
    SetGlobal("Iajamis06","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  CheckStatLT(LastSeenBy(Myself),90,RESISTSLASHING)
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
  RandomNumGT(6,4)
  Range(LastSeenBy(Myself),4)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(ThirdNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),2,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END