//Shadow Temple //

IF
  InMyArea(Player1)
  OR(4)
   SpellCast([PC],WIZARD_DEATH_FOG)
   SpellCast([PC],WIZARD_CLOUDKILL)
   SpellCast([PC],WIZARD_INCENDIARY_CLOUD)
   SpellCast([PC],WIZARD_STINKING_CLOUD)
THEN
  RESPONSE #100
    SetGlobal("Iapccloud","AR1401",1)
    SetGlobalTimer("Iacloudtime","AR1401",60)
END

IF
  Global("Iapccloud","AR1401",1)
  GlobalTimerExpired("Iacloudtime","AR1401")
THEN
  RESPONSE #100
    SetGlobal("Iapccloud","AR1401",0)
END

IF
  OpenState("DoorMediumRoom1",TRUE)
  Global("Door1Opened","AR1401",0)
THEN
  RESPONSE #100
    SetGlobal("Door1Opened","AR1401",1)
END

IF
  OpenState("DoorMediumRoom1",FALSE)
  Global("Door1Opened","AR1401",1)
THEN
  RESPONSE #100
    OpenDoor("DoorMediumRoom1")
END

IF
  Global("Ianoexit","AR1401",1)
THEN
  RESPONSE #100
    SetGlobal("Ianoexit","AR1401",2)
    TriggerActivation("Tran1400",FALSE)
    TriggerActivation("Tran1402",FALSE)
END

IF
  Global("Ianoexit","AR1401",2)
  Dead("rumar01") //
  Dead("rumar03") //
THEN
  RESPONSE #100
    SetGlobal("Ianoexit","AR1401",3)
    TriggerActivation("Tran1400",TRUE)
    TriggerActivation("Tran1402",TRUE)
END

IF
  Global("Iaancholwat","GLOBAL",2)
  Global("Iaexit2","AR1401",0)
THEN
  RESPONSE #100
    SetGlobal("Iaexit2","AR1401",1)
    SetGlobalTimer("Iareopen","AR1401",12)
    TriggerActivation("CrystalTrigger",FALSE)
    TriggerActivation("Tran1400",FALSE)
    TriggerActivation("Tran1402",FALSE)
END

IF
  Global("Iaexit2","AR1401",1)
  GlobalTimerExpired("Iareopen","AR1401")
  Dead("S!shadm1")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("Iaexit2","AR1401",2)
    SetGlobal("Iaancholwat","GLOBAL",3)
    TriggerActivation("CrystalTrigger",TRUE)
    TriggerActivation("Tran1400",TRUE)
    TriggerActivation("Tran1402",TRUE)
END

IF
  Class(Player1,RANGER_ALL)
  GlobalGT("UmarPlotStarted","AR1401",0)
  Global("Iarangerjob","GLOBAL",0)
  Dead("rumar01") // ~Umar~
THEN
  RESPONSE #100
    SetGlobal("Iarangerjob","GLOBAL",1)
END

IF
   Global("Iaitem","AR1401",0)
THEN
   RESPONSE #100
      SetGlobal("Iaitem","AR1401",1)
      ActionOverride("Container 12",CreateItem("scrl8g",0,0,0)) //
      ActionOverride("Container 12",CreateItem("scrl3g",0,0,0)) //
      ActionOverride("Container 4",CreateItem("potn07",0,0,0)) //
END

IF
  Global("Iaexit","AR1401",0)
  Global("ShadowLordDead","GLOBAL",0)
  Global("Deactivate","AR1401",1)
THEN
  RESPONSE #100
    SetGlobal("Iaexit","AR1401",1)
    TriggerActivation("Tran1404",FALSE)
END

IF
  Global("Iaexit","AR1401",1)
  Global("ShadowLordDead","GLOBAL",0)
  Global("Deactivate","AR1401",1)
  Dead("RNGSHA04")
THEN
  RESPONSE #100
    SetGlobal("Iaexit","AR1401",2)
    TriggerActivation("Tran1404",TRUE)
END

IF
  Global("Ianticheat","AR1401",0)
  GlobalGT("UmarPlotStarted","AR1401",0)
  Global("Ianoexit","AR1401",0)
  Exists("rumar03") //
  !Dead("rumar03") //
  !Allegiance("rumar03",ENEMY)
  HPPercentLT("rumar03",100)
THEN
  RESPONSE #100
    SetGlobal("Ianticheat","AR1401",1)
    ChangeEnemyAlly("rumar03",ENEMY)
    ApplySpellRES("SPWII07","rumar03")
END

IF
   Global("Ianoexit","AR1401",2)
   Dead("rumar03")
   Exists("rumar01") //
  !Dead("rumar01") //
   Global("Iaitemremove","AR1401",0)
THEN
   RESPONSE #100
      SetGlobal("Iaitemremove","AR1401",1)
      TakeItemReplace("amul12","S!ctrly","rumar01")
END