// Helter Steps //

IF
  InMyArea(Player1)
  OnCreation()
  Global("IAPlot","S!0001",0)
  Global("IADruidPlot","GLOBAL",10)
THEN
  RESPONSE #100
    TriggerActivation("Cave1",FALSE)
    TriggerActivation("Cave2",FALSE)
    TriggerActivation("Cave3",FALSE)
    TriggerActivation("Swamp",FALSE)
    ClearAllActions()
    StartCutSceneMode()
    StartCutSceneEx("S!druc12",TRUE)
END

// the first two waves
IF
  InMyArea(Player1)
  Global("IAPlot","S!0001",1)
  GlobalLT("IaWaves","S!0001",2)
  !GlobalTimerNotExpired("IaWaveTimer","S!0001")
THEN
  RESPONSE #100
    IncrementGlobal("IaWaves","S!0001",1)
    SetGlobalTimer("IaWaveTimer","S!0001",12)

    CreateCreature("weregr01",[3820.780],0)
    CreateCreature("weregr01",[3820.780],0)

    CreateCreature("weregr01",[3675.1635],0)
    CreateCreature("weregr01",[3675.1635],0)

    CreateCreature("weregr01",[2580.1520],0)
    CreateCreature("weregr01",[2580.1520],0)

    CreateCreature("weregr01",[2525.740],0)
    CreateCreature("weregr01",[2525.740],0)
END

// the 3rd wave
IF
  InMyArea(Player1)
  Global("IAPlot","S!0001",1)
  GlobalGT("IaWaves","S!0001",1)
  GlobalLT("IaWaves","S!0001",3)
  !GlobalTimerNotExpired("IaWaveTimer","S!0001")
THEN
  RESPONSE #100
    IncrementGlobal("IaWaves","S!0001",1)
    SetGlobalTimer("IaWaveTimer","S!0001",18)
    CreateCreature("S!loup01",[3820.780],0)
    CreateCreature("S!loup01",[3675.1635],0)
    CreateCreature("S!loup01",[2580.1520],0)
    CreateCreature("S!loup01",[2525.740],0)
END

// after the 3rd wave
IF
  InMyArea(Player1)
  OR(2)
    Global("IAPlot","S!0001",1)
    Global("IAPlot","S!0001",2)
  Global("IaWaves","S!0001",3)
  !GlobalTimerNotExpired("IaWaveTimer","S!0001")
THEN
  RESPONSE #100
    SetGlobalTimer("IaWaveTimer","S!0001",18)
    CreateCreature("S!wereg1",[3820.780],0)
    CreateCreature("S!wereg1",[3675.1635],0)
    CreateCreature("S!wereg1",[2580.1520],0)
    CreateCreature("S!wereg1",[2525.740],0)
END

// if felaern perishes - temporary solution
IF
  InMyArea(Player1)
  OR(2)
    Global("IAPlot","S!0001",1)
    Global("IAPlot","S!0001",2)
  Global("Iakillp1","S!0001",0)
  Dead("S!gdru03")
THEN
  RESPONSE #100
    SetGlobal("Iakillp1","S!0001",1)
    ClearAllActions()
    StartCutSceneMode()
    Wait(2)
    MoveViewObject(Player1,-1)
    DisplayStringHead(Player1,@12084)
    Wait(4)
    EndCutSceneMode()
    Kill(Player1)
END

// kick-off timer for Ferrou after the 3th wave
IF
  InMyArea(Player1)
  Global("IAPlot","S!0001",1)
  Global("IaWaves","S!0001",3)
  Global("IACombat","S!0001",0)
THEN
  RESPONSE #100
    SetGlobal("IACombat","S!0001",1)
    SetGlobalTimer("IaFerrouComes","S!0001",6)
END

// Ferrou appears
IF
  InMyArea(Player1)
  Global("IAPlot","S!0001",1)
  Global("IACombat","S!0001",1)
  GlobalTimerExpired("IaFerrouComes","S!0001")
THEN
  RESPONSE #100
    SetGlobal("IACombat","S!0001",2)
    ClearAllActions()
    StartCutSceneMode()
    MoveViewObject(Player1,INSTANT)
    Wait(1)
    CreateCreatureObjectDoor("S!ferr01",Player1,0,0,0)
    Wait(3)
    ActionOverride("S!ferr01",StartDialogueNoSet(Player1))
END

// heavily injured Ferrou escapes
IF
  InMyArea(Player1)
  Global("IAPlot","S!0001",2)
  Exists("S!ferele")
  !Dead("S!ferele")
  HPPercentLT("S!ferele",10)
THEN
  RESPONSE #100
    SetGlobal("IAPlot","S!0001",3)
    ClearAllActions()
    StartCutSceneMode()
    MoveViewObject("S!ferele",INSTANT)
    Wait(2)
    ActionOverride("S!ferele",ReallyForceSpellRES("S!dru06",Myself))
    Wait(2)
    ActionOverride("S!gdru03",StartDialogueNoSet(Player1))
END

// quest finale //

IF
  InMyArea(Player1)
  Global("IADruidPlot","GLOBAL",15)
  Global("IAFinale","S!0001",0)
THEN
  RESPONSE #100
    SetGlobal("IAFinale","S!0001",1)
    ActionOverride("S!gdru04",EscapeArea())
    AddXPObject(Player1,50000)
    AddXPObject(Player2,50000)
    AddXPObject(Player3,50000)
    AddXPObject(Player4,50000)
    AddXPObject(Player5,50000)
    AddXPObject(Player6,50000)
    EraseJournalEntry(@5510)
    EraseJournalEntry(@5511)
    EraseJournalEntry(@5512)
    EraseJournalEntry(@5513)
    EraseJournalEntry(@5514)
    EraseJournalEntry(@5515)
    EraseJournalEntry(@5516)
    EraseJournalEntry(@5517)
    EraseJournalEntry(@5519)
    EraseJournalEntry(@5520)
    EraseJournalEntry(@5521)
    EraseJournalEntry(@5522)
    EraseJournalEntry(@5523)
    EraseJournalEntry(@5525)
    EraseJournalEntry(@5526)
    EraseJournalEntry(@5527)
    EraseJournalEntry(@5528)
    AddJournalEntry(@5529,QUEST_DONE)
END
