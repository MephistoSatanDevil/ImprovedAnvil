// start the combat
IF
  InMyArea(Player1)
  Global("IAPlot","S!0005",0)
  Global("IADruidPlot","GLOBAL",13)
  PartyHasItem("S!misc73")
THEN
  RESPONSE #100
    SetGlobal("IAPlot","S!0005",1)
    TriggerActivation("TranS0006",FALSE)
    ClearAllActions()
    StartCutSceneMode()
    StartCutScene("S!druc14")
END

IF
  InMyArea(Player1)
  Global("IAPlot","S!0005",4)
  Global("IaPhrase","S!0005",0)
THEN
  RESPONSE #100
    SetGlobal("IaPhrase","S!0005",1)
    DisplayString("S!gdru04",@5990)
END

IF
  InMyArea(Player1)
  Global("IAPlot","S!0005",4)
  GlobalLT("IAPortals","S!0005",5)
  !GlobalTimerNotExpired("IaPortalsTimer","S!0005")
THEN
  RESPONSE #100
    IncrementGlobal("IAPortals","S!0005",1)
    SetGlobalTimer("IaPortalsTimer","S!0005",18)
    CreateCreatureDoor("S!dabbla",[1955.1940],0)
    CreateCreatureDoor("S!dabgre",[920.1920],0)
    CreateCreatureDoor("S!dabred",[270.410],0)
    CreateCreatureDoor("S!dabblu",[1250.135],0)
    CreateCreatureDoor("S!dabwhi",[110.1030],0)
END

IF
  InMyArea(Player1)
  Global("IAPlot","S!0005",4)
  Global("IAPortals","S!0005",5)
  NumDeadGT("S!dabbla",4)
  NumDeadGT("S!dabgre",4)
  NumDeadGT("S!dabred",4)
  NumDeadGT("S!dabblu",4)
  NumDeadGT("S!dabwhi",4)
THEN
  RESPONSE #100
    SetGlobal("IAPortals","S!0005",6)
    ReallyForceSpellRES("SPWII07","S!dracof")
    TakeItemReplace("minhp1","S!minhp3","S!dracof")
END

// transition into the final cutscene
IF
  InMyArea(Player1)
  Global("IAPlot","S!0005",4)
  Global("IAPortals","S!0005",6)
  Global("IADruidPlot","GLOBAL",13)
  !Dead("S!dracof")
  HPLT("S!dracof",10)
THEN
  RESPONSE #100
    SetGlobal("IAPlot","S!0005",5)
    ClearAllActions()
    StartCutSceneMode()
    CutSceneId(Player1)
    Wait(1)
    ActionOverride("S!dracof",ChangeEnemyAlly(Myself,NEUTRAL))
    Wait(1)
    ActionOverride("S!dracof",StartDialogueNoSet(Player1))
    EndCutSceneMode()
END

// if felaern perishes, the encounter is lost
IF
  InMyArea(Player1)
  Global("Iakillp1","S!0005",0)
  Dead("S!gdru04")
THEN
  RESPONSE #100
    SetGlobal("Iakillp1","S!0005",1)
    ClearAllActions()
    StartCutSceneMode()
    Wait(2)
    MoveViewObject(Player1,-1)
    DisplayStringHead(Player1,@12096)
    Wait(4)
    EndCutSceneMode()
    Kill(Player1)
END