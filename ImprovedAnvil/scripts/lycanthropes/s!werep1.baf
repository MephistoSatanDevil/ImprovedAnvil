// prep script for werewolf AI (young & regular werewolf) //
// set up variables and common blocks to be used in the primary AI script later //

IF
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",0)
  !HPLT(Myself,1)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    SetGlobal("IaRegenTotalHits","LOCALS",1)
    SetGlobal("IaCanPanic","LOCALS",1)
    SetGlobal("IaCanHowl","LOCALS",0)
    SetGlobal("IaCanGaze","LOCALS",0)
    SetGlobal("IaCanSummon","LOCALS",0)
END

// prevent werewolves in the druid grove ambush from running away in panic
IF
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",1)
  Global("IaOnce1","LOCALS",0)
  Name("S!were02",Myself)
THEN
  RESPONSE #100
    SetGlobal("IaOnce1","LOCALS",1)
    SetGlobal("IaCanPanic","LOCALS",0)
END

// this forces werewolves to escape the area at the end of trademeet siege event //
IF
  Allegiance(Myself,ENEMY)
  AreaCheck("S!0007")
  Global("IADruidSiege","GLOBAL",3)
  Global("IaFlee","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("IaFlee","LOCALS",1)
    ChangeAIScript("",GENERAL)
    EscapeArea()
END