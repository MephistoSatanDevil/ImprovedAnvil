// prep script for werewolf AI (greater werewolf) //
// set up variables and common blocks to be used in the primary AI script later //

IF
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",0)
  !HPLT(Myself,1)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    SetGlobal("IaRegenTotalHits","LOCALS",3)
    SetGlobal("IaCanPanic","LOCALS",1)
    SetGlobal("IaCanHowl","LOCALS",1)
    SetGlobal("IaHowlTotalAmount","LOCALS",3)
    SetGlobal("IaCanGaze","LOCALS",0)
    SetGlobal("IaCanSummon","LOCALS",0)
END

// prevent Anath and Captain from panicking
IF
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",1)
  Global("IaOnce1","LOCALS",0)
  OR(2)
    Name("rngwlf04",Myself)
    Name("firamb01",Myself)
THEN
  RESPONSE #100
    SetGlobal("IaOnce1","LOCALS",1)
    SetGlobal("IaCanPanic","LOCALS",0)
END

// this forces werewolves to escape the area at the end of trademeet siege event //
IF
  Allegiance(Myself,ENEMY)
  AreaCheck("S!0007")
  Global("IADruidSiege","GLOBAL",3)
  Global("IaFlee","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("IaFlee","LOCALS",1)
    ChangeAIScript("",GENERAL)
    EscapeArea()
END

// this forces werewolves to escape the area at the end of helter steps event //
IF
  Allegiance(Myself,ENEMY)
  AreaCheck("S!0001")
  GlobalGT("IAPlot","S!0001",2)
  Global("IaFlee","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("IaFlee","LOCALS",1)
    ChangeAIScript("",GENERAL)
    EscapeArea()
END