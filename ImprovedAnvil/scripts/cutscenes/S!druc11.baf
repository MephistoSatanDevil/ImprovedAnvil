// Extended druid stronghold, combat encounters through Small Teeth Pass //

// movement towards the bridge after the initial dialogue
IF
  Global("IADruidPlot","GLOBAL",10)
  Global("IAFight","AR1700",0)
THEN
  RESPONSE #100
    CutSceneId(Player1)

    SetGlobal("IAFight","AR1700",1)
    SetAreaScript("S!skipc",OVERRIDE)
    SetGlobal("IA_CUTSCENE_BREAKABLE","GLOBAL",1101)
    SetCutSceneBreakable(TRUE)

    Wait(1)
    FadeToColor([20.0],0)
    Wait(1)

    JumpToPoint([3120.1750])
    MoveViewObject(Myself,INSTANT)
    ActionOverride("S!tslead",JumpToPoint([2940.1710]))
    ActionOverride("S!tslead",FaceObject(Player1))

    ActionOverride(Player2,JumpToPoint([3245.1690]))
    ActionOverride(Player2,FaceObject("s!tslead"))
    ActionOverride(Player3,JumpToPoint([3225.1580]))
    ActionOverride(Player3,FaceObject("s!tslead"))
    ActionOverride(Player4,JumpToPoint([3080.1530]))
    ActionOverride(Player4,FaceObject("s!tslead"))
    ActionOverride(Player5,JumpToPoint([2930.1510]))
    ActionOverride(Player5,FaceObject("s!tslead"))
    ActionOverride(Player6,JumpToPoint([2980.1400]))
    ActionOverride(Player6,FaceObject("s!tslead"))

    Wait(1)
    FadeFromColor([20.0],0)
    Wait(2)

    // dialog at the bridge
    FaceObject("S!tslead")
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5773))
    Wait(3)
    DisplayStringHead(Myself,@5774)
    Wait(3)

    MoveViewPoint([1660.2480],VERY_FAST)
    ActionOverride("S!tslead",MoveToPointNoInterrupt([1935.2490]))
    ActionOverride(Player2,MoveToPointNoInterrupt([1745.2400]))
    ActionOverride(Player3,MoveToPointNoInterrupt([2050.2550]))
    ActionOverride(Player4,MoveToPointNoInterrupt([2270.2530]))
    ActionOverride(Player5,MoveToPointNoInterrupt([2210.2305]))
    ActionOverride(Player6,MoveToPointNoInterrupt([2290.2365]))
    MoveToPointNoInterrupt([1660.2480])

    FaceObject("S!tslead")
    ActionOverride("S!tslead",FaceObject(Player1))
    ActionOverride(Player2,FaceObject("s!tslead"))
    ActionOverride(Player3,FaceObject("s!tslead"))
    ActionOverride(Player4,FaceObject("s!tslead"))
    ActionOverride(Player5,FaceObject("s!tslead"))
    ActionOverride(Player6,FaceObject("s!tslead"))

    Wait(12)
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5783))
    Wait(3)

    SetCutSceneBreakable(FALSE)
    SetGlobal("IA_CUTSCENE_BREAKABLE","GLOBAL",0)
    SetAreaScript("",OVERRIDE)
    SetGlobalTimer("IaFirstAmbushTimer","AR1700",12)

    MultiPlayerSync()
    EndCutSceneMode()
END

// movement after the fight at the bridge
IF
  Global("IADruidPlot","GLOBAL",10)
  Global("IAFight","AR1700",2)
THEN
  RESPONSE #100
    CutSceneId(Player1)

    Wait(1)
    FadeToColor([20.0],0)
    Wait(1)

    JumpToPoint([730.2660])
    MoveViewObject(Myself,INSTANT)
    ActionOverride("S!tslead",JumpToPoint([630.2615]))
    ActionOverride("S!tslead",FaceObject(Player1))

    ActionOverride(Player2,JumpToPoint([830.2520]))
    ActionOverride(Player2,FaceObject("s!tslead"))
    ActionOverride(Player3,JumpToPoint([830.2605]))
    ActionOverride(Player3,FaceObject("s!tslead"))
    ActionOverride(Player4,JumpToPoint([690.2700]))
    ActionOverride(Player4,FaceObject("s!tslead"))
    ActionOverride(Player5,JumpToPoint([545.2685]))
    ActionOverride(Player5,FaceObject("s!tslead"))
    ActionOverride(Player6,JumpToPoint([500.2560]))
    ActionOverride(Player6,FaceObject("s!tslead"))

    Wait(1)
    FadeFromColor([20.0],0)
    Wait(2)

    // dialog before the ambush
    FaceObject("S!tslead")
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5775))
    Wait(3)
    DisplayStringHead(Myself,@5776)
    Wait(3)
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5784))
    Wait(3)

    ActionOverride("S!tslead",DestroyItem("minhp1"))
    SetGlobal("IAFight","AR1700",3)
    SetGlobalTimer("IaSecondAmbushTimer","AR1700",12)

    MultiPlayerSync()
    EndCutSceneMode()
END

// movement after the ambush, Dram Kallen survives
IF
  Global("IADruidPlot","GLOBAL",10)
  Global("IAFight","AR1700",4)
  Exists("S!tslead")
  !Dead("S!tslead")
THEN
  RESPONSE #100
    CutSceneId(Player1)

    Wait(1)
    FadeToColor([20.0],0)
    Wait(1)

    JumpToPoint([555.3060])
    ActionOverride("S!tslead",JumpToPoint([765.3040]))
    ActionOverride("S!tslead",FaceObject(Player1))
    MoveViewObject(Myself,INSTANT)

    Wait(1)
    FadeFromColor([20.0],0)
    Wait(2)

    FaceObject("S!tslead")
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5777))
    Wait(3)
    DisplayStringHead(Myself,@5778)
    Wait(3)
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5779))
    Wait(3)
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5780))
    Wait(3)

    ActionOverride("S!tslead",MoveToPointNoInterrupt([645.3075]))
    ActionOverride("S!tslead",DestroySelf())
    Wait(3)

    AddJournalEntry(@5526,QUEST)
    EndCutSceneMode()
END

// movement after the ambush, Dram Kallen perished
IF
  Global("IADruidPlot","GLOBAL",10)
  Global("IAFight","AR1700",4)
  Exists("S!tslead")
  Dead("S!tslead")
THEN
  RESPONSE #100
    CutSceneId(Player1)

    Wait(1)
    FadeToColor([20.0],0)
    Wait(1)

    JumpToPoint([555.3040])
    MoveViewObject(Myself,INSTANT)

    Wait(1)
    FadeFromColor([20.0],0)
    Wait(2)

    DisplayStringHead(Myself,@5781)
    Wait(3)
    DisplayStringHead(Myself,@5782)
    Wait(3)

    AddJournalEntry(@5526,QUEST)
    SetGlobal("IADramDies","GLOBAL",1)
    EndCutSceneMode()
END