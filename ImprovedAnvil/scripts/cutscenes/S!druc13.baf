// Extended druid stronghold, Helter Steps, Felearn, casts enchantment from the scroll and teleports the party to the final area //

IF
  TRUE()
THEN
  RESPONSE #100
    CutSceneId(Player1)

    SetGlobal("IAPlot","S!0001",6)

    Wait(1)
    MoveViewObject("S!gdru03",INSTANT)
    Wait(1)

    // TODO: adjust spell sounds to mimic other teleport-style spells?
    ActionOverride("S!gdru03",ForceSpellRES("S!DRU03",Myself))
    Wait(5)

    ForceSpell(Myself,DRYAD_TELEPORT)
    ActionOverride("S!gdru03",ForceSpell(Myself,DRYAD_TELEPORT))
    ActionOverride(Player2,ForceSpell(Myself,DRYAD_TELEPORT))
    ActionOverride(Player3,ForceSpell(Myself,DRYAD_TELEPORT))
    ActionOverride(Player4,ForceSpell(Myself,DRYAD_TELEPORT))
    ActionOverride(Player5,ForceSpell(Myself,DRYAD_TELEPORT))
    ActionOverride(Player6,ForceSpell(Myself,DRYAD_TELEPORT))

    Wait(1)
    FadeToColor([20.0],0)
    Wait(1)

    ActionOverride("s!gdru03",DestroySelf())

    LeaveAreaLUAPanic("S!0005","",[1630.960],W)
    LeaveAreaLUA("S!0005","",[1630.960],W)

    ActionOverride(Player2,LeaveAreaLUA("S!0005","",[1570.1000],W))
    ActionOverride(Player3,LeaveAreaLUA("S!0005","",[1485.1015],W))
    ActionOverride(Player4,LeaveAreaLUA("S!0005","",[1420.975],W))
    ActionOverride(Player5,LeaveAreaLUA("S!0005","",[1385.925],W))
    ActionOverride(Player6,LeaveAreaLUA("S!0005","",[1415.860],W))
    MoveViewObject(Myself,INSTANT)

    CreateCreature("s!gdru04",[1515.910],0)

    FaceObject("S!gdru04")
    ActionOverride("S!gdru04",FaceObject(Myself))
    ActionOverride(Player2,FaceObject("S!gdru04"))
    ActionOverride(Player3,FaceObject("S!gdru04"))
    ActionOverride(Player4,FaceObject("S!gdru04"))
    ActionOverride(Player5,FaceObject("S!gdru04"))
    ActionOverride(Player6,FaceObject("S!gdru04"))

    Explore()
    Wait(1)
    FadeFromColor([20.0],0)
    Wait(1)

    ActionOverride("S!gdru04",DisplayStringHead(Myself,@5860))
    Wait(3)
    ActionOverride("S!gdru04",DisplayStringHead(Myself,@5861))
    Wait(3)
    ActionOverride("S!gdru04",DisplayStringHead(Myself,@5862))
    Wait(3)

    AddJournalEntry(@5528,QUEST)

    MultiPlayerSync()
    EndCutSceneMode()
END