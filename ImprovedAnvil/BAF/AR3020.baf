// close the portal for the duration of the battle if the player hasn't read the ritual scroll //
IF
  InMyArea(Player1)
	Global("DemogorgonChange","GLOBAL",1)
  Global("Iaexit","AR3020",0)
  Global("OdrenRitualDone","GLOBAL",0)
THEN
	RESPONSE #100
    SetGlobal("Iaexit","AR3020",1)
    TriggerActivation("Tran3019",FALSE)
		TriggerActivation("Tran3000",FALSE)
		AmbientActivate("AMB_Portal1",FALSE)
		AmbientActivate("AMB_Portal2",FALSE)
		SoundActivate("PortalL",FALSE)
		SoundActivate("PortalR",FALSE)
END

// Periodic spawning of bombers while Rozvankee is alive //

IF
  InMyArea(Player1)
  Global("DemogorgonChange","GLOBAL",1)
  Global("IaInitBombers","AR3020",0)
  Exists("S!rozvan")
  !Dead("S!rozvan")
THEN
	RESPONSE #100
    SetGlobal("IaInitBombers","AR3020",1)
    SetGlobalTimer("IaBombers","AR3020",8)
END

IF
  InMyArea(Player1)
  Global("DemogorgonChange","GLOBAL",1)
  Global("IaInitBombers","AR3020",1)
  Exists("S!rozvan")
  Dead("S!rozvan")
THEN
	RESPONSE #100
    SetGlobal("IaInitBombers","AR3020",2)
END

IF
  InMyArea(Player1)
  Global("DemogorgonChange","GLOBAL",1)
  Global("IaInitBombers","AR3020",1)
  GlobalTimerExpired("IaBombers","AR3020")
THEN
	RESPONSE #100
    SetGlobalTimer("IaBombers","AR3020",8)
    DisplayStringHead("S!rozvan",70998) // Come, my minions!
    CreateCreature("S!dembom",[1287.1118],0)
    CreateCreature("S!dembom",[663.1150],0)
    CreateCreature("S!dembom",[795.771],0)
END

// init variables for Nulonga's resurrection ability //

IF
  InMyArea(Player1)
  Global("DemogorgonChange","GLOBAL",1)
  Global("IaInitNulonga","AR3020",0)
THEN
	RESPONSE #100
    SetGlobal("IaInitNulonga","AR3020",1)
    SetGlobal("IaResurrections","AR3020",3)
    SetGlobal("IaNulongaFinallyDead","AR3020",0)
END

// set the global variable if Nulonga's true form has died, to prevent further appearance of his clones //

IF
  InMyArea(Player1)
  Global("IaNulongaFinallyDead","AR3020",0)
  Dead("S!Nulong")
THEN
	RESPONSE #100
    SetGlobal("IaNulongaFinallyDead","AR3020",1)
END

// raise the clones when somebody dies //

IF
  InMyArea(Player1)
  Global("IaInitNulonga","AR3020",1)
  Global("IaNulongaFinallyDead","AR3020",0)
  Global("IaDeadBelch","AR3020",0)
  GlobalGT("IaResurrections","AR3020",0)
  Dead("S!belch")
  !Exists("S!nulon1")
  !GlobalTimerNotExpired("IaResurrectionTimer","AR3020")
THEN
	RESPONSE #100
    IncrementGlobal("IaResurrections","AR3020",-1)
    SetGlobal("IaDeadBelch","AR3020",1)
    DisplayStringHeadDead("S!nulong",21077)
    CreateVisualEffect("SPRAISED",[1006.799])
    Wait(4)
    CreateCreature("S!Nulon1",[1006.799],0)
    ActionOverride("S!Nulon1",FaceObject("DEMOGOR2"))
    SetGlobalTimer("IaResurrectionTimer","AR3020",6)
END

IF
  InMyArea(Player1)
  Global("IaInitNulonga","AR3020",1)
  Global("IaNulongaFinallyDead","AR3020",0)
  Global("IaDeadKazuul","AR3020",0)
  GlobalGT("IaResurrections","AR3020",0)
  Dead("S!kazuul")
  !Exists("S!nulon2")
  !GlobalTimerNotExpired("IaResurrectionTimer","AR3020")
THEN
	RESPONSE #100
    IncrementGlobal("IaResurrections","AR3020",-1)
    SetGlobal("IaDeadKazuul","AR3020",1)
    DisplayStringHeadDead("S!nulong",21077)
    CreateVisualEffect("SPRAISED",[737.904])
    Wait(4)
    CreateCreature("S!Nulon2",[737.904],0)
    ActionOverride("S!Nulon2",FaceObject("DEMOGOR2"))
    SetGlobalTimer("IaResurrectionTimer","AR3020",6)
END

IF
  InMyArea(Player1)
  Global("IaInitNulonga","AR3020",1)
  Global("IaNulongaFinallyDead","AR3020",0)
  Global("IaDeadKargoth","AR3020",0)
  GlobalGT("IaResurrections","AR3020",0)
  Dead("S!kargot")
  !Exists("S!nulon3")
  !GlobalTimerNotExpired("IaResurrectionTimer","AR3020")
THEN
	RESPONSE #100
    IncrementGlobal("IaResurrections","AR3020",-1)
    SetGlobal("IaDeadKargoth","AR3020",1)
    DisplayStringHeadDead("S!nulong",21077)
    CreateVisualEffect("SPRAISED",[1096.1164])
    Wait(4)
    CreateCreature("S!Nulon3",[1096.1164],0)
    ActionOverride("S!Nulon3",FaceObject("DEMOGOR2"))
    SetGlobalTimer("IaResurrectionTimer","AR3020",6)
END

IF
  InMyArea(Player1)
  Global("IaInitNulonga","AR3020",1)
  Global("IaNulongaFinallyDead","AR3020",0)
  Global("IaDeadRozvankee","AR3020",0)
  GlobalGT("IaResurrections","AR3020",0)
  Dead("S!rozvan")
  !Exists("S!nulon4")
  !GlobalTimerNotExpired("IaResurrectionTimer","AR3020")
THEN
	RESPONSE #100
    IncrementGlobal("IaResurrections","AR3020",-1)
    SetGlobal("IaDeadRozvankee","AR3020",1)
    DisplayStringHeadDead("S!nulong",21077)
    CreateVisualEffect("SPRAISED",[770.1130])
    Wait(4)
    CreateCreature("S!Nulon4",[770.1130],0)
    ActionOverride("S!Nulon4",FaceObject("DEMOGOR2"))
    SetGlobalTimer("IaResurrectionTimer","AR3020",6)
END
