// close the portal for the duration of the battle if the player hasn't read the ritual scroll //
IF
  InMyArea(Player1)
	Global("DemogorgonChange","GLOBAL",1)
  Global("Iaexit","AR3020",0)
  Global("OdrenRitualDone","GLOBAL",0)
THEN
	RESPONSE #100
    SetGlobal("Iaexit","AR3020",1)
    TriggerActivation("Tran3019",FALSE)
		TriggerActivation("Tran3000",FALSE)
		AmbientActivate("AMB_Portal1",FALSE)
		AmbientActivate("AMB_Portal2",FALSE)
		SoundActivate("PortalL",FALSE)
		SoundActivate("PortalR",FALSE)
END

// init variables for Nulonga's resurrection ability //

IF
  InMyArea(Player1)
  Global("DemogorgonChange","GLOBAL",1)
  Global("IaInitNulonga","AR3020",0)
THEN
	RESPONSE #100
    SetGlobal("IaInitNulonga","AR3020",1)
    SetGlobal("IaEnemiesDead","AR3020",0)
    SetGlobal("IaResurrections","AR3020",3)
    SetGlobal("IaNulongaFinallyDead","AR3020",0)
END

// increase the counter for dead enemies when demogorgon's comrades die //

IF
  InMyArea(Player1)
  Global("IaInitNulonga","AR3020",1)
  Global("IaDeadBelch","AR3020",0)
  Dead("S!belch")
THEN
	RESPONSE #100
    SetGlobal("IaDeadBelch","AR3020",1)
    IncrementGlobal("IaEnemiesDead","AR3020",1)
END

IF
  InMyArea(Player1)
  Global("IaInitNulonga","AR3020",1)
  Global("IaDeadKazuul","AR3020",0)
  Dead("S!kazuul")
THEN
	RESPONSE #100
    SetGlobal("IaDeadKazuul","AR3020",1)
    IncrementGlobal("IaEnemiesDead","AR3020",1)
END

IF
  InMyArea(Player1)
  Global("IaInitNulonga","AR3020",1)
  Global("IaDeadKargoth","AR3020",0)
  Dead("S!kargot")
THEN
	RESPONSE #100
    SetGlobal("IaDeadKargoth","AR3020",1)
    IncrementGlobal("IaEnemiesDead","AR3020",1)
END

IF
  InMyArea(Player1)
  Global("IaInitNulonga","AR3020",1)
  Global("IaDeadRozvankee","AR3020",0)
  Dead("S!rozvan")
THEN
	RESPONSE #100
    SetGlobal("IaDeadRozvankee","AR3020",1)
    IncrementGlobal("IaEnemiesDead","AR3020",1)
END

// fire Nulonga's resurrections one by one //

IF
  InMyArea(Player1)
  GlobalGT("IaEnemiesDead","AR3020",0)
  GlobalGT("IaResurrections","AR3020",0)
  Global("IaNulongaFinallyDead","AR3020",0)
  Dead("S!nulong")
  !Exists("S!nulon1")
  !Exists("S!nulon2")
  !Exists("S!nulon3")
THEN
	RESPONSE #100
    IncrementGlobal("IaEnemiesDead","AR3020",-1)
    IncrementGlobal("IaResurrections","AR3020",-1)
    DisplayStringHeadDead("S!nulong",21077)
    CreateVisualEffect("SPRAISED",[1222.948])
    Wait(4)
    CreateCreature("S!Nulon1",[1222.948],0)
    ActionOverride("S!Nulon1",FaceObject("DEMOGOR2"))
END

IF
  InMyArea(Player1)
  GlobalGT("IaEnemiesDead","AR3020",0)
  GlobalGT("IaResurrections","AR3020",0)
  Global("IaNulongaFinallyDead","AR3020",0)
  Dead("S!nulong")
  Dead("S!nulon1")
  !Exists("S!nulon2")
  !Exists("S!nulon3")
THEN
	RESPONSE #100
    IncrementGlobal("IaEnemiesDead","AR3020",-1)
    IncrementGlobal("IaResurrections","AR3020",-1)
    DisplayStringHeadDead("S!nulon1",21077)
    CreateVisualEffect("SPRAISED",[1222.948])
    Wait(4)
    CreateCreature("S!Nulon2",[1222.948],0)
    ActionOverride("S!Nulon2",FaceObject("DEMOGOR2"))
END

IF
  InMyArea(Player1)
  GlobalGT("IaEnemiesDead","AR3020",0)
  GlobalGT("IaResurrections","AR3020",0)
  Global("IaNulongaFinallyDead","AR3020",0)
  Dead("S!nulong")
  Dead("S!nulon1")
  Dead("S!nulon2")
  !Exists("S!nulon3")
THEN
	RESPONSE #100
    IncrementGlobal("IaEnemiesDead","AR3020",-1)
    IncrementGlobal("IaResurrections","AR3020",-1)
    DisplayStringHeadDead("S!nulon2",21077)
    CreateVisualEffect("SPRAISED",[1222.948])
    Wait(4)
    CreateCreature("S!Nulon3",[1222.948],0)
    ActionOverride("S!Nulon3",FaceObject("DEMOGOR2"))
END

// set the global variable if all Nulonga's reincarnations are finally dead //

IF
  InMyArea(Player1)
  Global("IaResurrections","AR3020",0)
  Global("IaNulongaFinallyDead","AR3020",0)
  Dead("S!Nulong")
  Dead("S!Nulon1")
  Dead("S!Nulon2")
  Dead("S!Nulon3")
THEN
	RESPONSE #100
    SetGlobal("IaNulongaFinallyDead","AR3020",1)
END